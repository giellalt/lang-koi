! ========================================================================== !
!!                        !!!Pronominal morphology                           
! ========================================================================== !

!! !!Closed class personal pronouns
! ------------------------------
LEXICON PRONOUN-TYPES
 PERS ;
! INTERR ;
! INDEFINITE-PRON ;
! NEG-PRON ;
! COLL-PRON ;


LEXICON PERS                        !!≈ * __@CODE@__ 
ме+Pron+Pers+Sg1:ме perssg1decl ;    !!≈ * __@CODE@__ ...
тэ+Pron+Pers+Sg2:ті perssg2decl ; 
сія+Pron+Pers+Sg3:с perssg3decl ;
ме+Pron+Pers+Pl1:ми perspl1decl ;
тэ+Pron+Pers+Pl2:ті perspl2decl ; 
сія+Pron+Pers+Pl3:н perspl3decl ;

LEXICON PERS-SG1_
+Pron+Pers+Sg1: perssg1decl ;
LEXICON PERS-SG2_
+Pron+Pers+Sg2: perssg2decl ;
LEXICON PERS-SG3_
+Pron+Pers+Sg3: perssg3decl ;

LEXICON PERS-PL1_
+Pron+Pers+Pl1: perspl1decl ;
LEXICON PERS-PL2_
+Pron+Pers+Pl2: perspl2decl ;
LEXICON PERS-PL3_
+Pron+Pers+Pl3: perspl3decl ;

LEXICON perssg1decl   !!≈ * __@CODE@__ 
+Nom: WordEnd ;
: GEN_лӧн-THEN-K ;
+Ela+PxSg1:нсьым WordEnd ; !The elative case is used in Sg1 and Sg2 
                 !for the functions of the ablative in other persons
+Dat:ным WordEnd ; !What should be done with the 4 dative forms?
+Dat:м WordEnd ;
+Dat:н WordEnd ;
:н DAT_лӧ-THEN-K ;
+Acc:нӧ WordEnd ;

:нам WordEnd ;
:нам GEN_лӧн-THEN-K ;
+Ins:намӧн WordEnd ;
+Com:намкӧт WordEnd ;
:нам CAR_тӧг-THEN-K ;
+Inе:намын WordEnd ;
:нам ELA_ісь-THEN-K ;
+Ill:намӧ WordEnd ;
:нам APR_сянь-THEN-K ;
+Ter:намӧдз WordEnd ;
: perssg ; ! for the regular obl sg

LEXICON perspl1decl    !!≈ * __@CODE@__
+Nom: WordEnd ;
+Nom:йӧ WordEnd ;
:ян WordEnd ;
+Acc:янӧс WordEnd ;
:ян persplobl ;
!!Extras form 2000 Grammar: 163
:ян GEN_лӧн-THEN-K ; !Synomymous with: миян
+PxPl1+Acc:яннымӧс WordEnd ; !Synomymous with: миянӧс
+Ela+PxPl1:янсьыным WordEnd ; !Synomymous with:миянлысь
+Com:кӧт WordEnd ; !Synomymous with: миянкӧт
+PxPl1+Com:яннымкӧт WordEnd ; !Synomymous with: миянкӧт
: CAR_тӧг-THEN-K ; !Synomymous with: миянтӧг
+Car+PxPl1:янтӧгным WordEnd ; !Synomymous with: миянтӧг
+PxPl1+Cns:яннымла WordEnd ; !Synomymous with: миянла
: ELA_ісь-THEN-K ; !Synomymous with: миянысь
+Ill:ӧ WordEnd ; !Synomymous with: миянӧ
+Apr+PxPl1:янланьным WordEnd ; !Synomymous with: миянлань
+Apr+PxPl1:ланьным WordEnd ; !Synomymous with: миянлань
+Egr+PxPl1:янсяньным WordEnd ; !Synomymous with: миянсянь
+TerI:ӧдзз WordEnd ; !Synomymous with: миянӧдз

LEXICON perssg2decl     !!≈ * __@CODE@__
+Nom: WordEnd ;
+Gen:нат WordEnd ;
+Dat:ныт WordEnd ;
 GEN_лӧн-THEN-K ;
+Ela+PxSg2:нсьыт WordEnd ;
+Dat:н WordEnd ;
+Dat:т WordEnd ;
:т DAT_лӧ-THEN-K ;
:н DAT_лӧ-THEN-K ;
:ныт DAT_лӧ-THEN-K ;
+Acc:нӧ WordEnd ;
:нат WordEnd ;
:нат GEN_лӧн-THEN-K ;
+Ins:натӧн WordEnd ;
+Com:накӧт WordEnd ;
:на CAR_тӧг-THEN-K ;
+Ine:натын WordEnd ;
:нат ELA_ісь-THEN-K ;
+Ill:натӧ WordEnd ;
+Egr:натсянь WordEnd ;
+Ter:натӧдз WordEnd ;
: perssg ;  ! for the regular obl sg

LEXICON perspl2decl !!≈ * __@CODE@__ 
+Nom: WordEnd ;
+Der/йӧ+Nom:йӧ WordEnd ; !We need a special tag for discerning +Nom forms
:ян DAT_лӧ-THEN-K ;

:ян WordEnd ;
:ян GEN_лӧн-THEN-K ;
+Acc:янӧс WordEnd ;
:ян persplobl ;

!More forms from Ӧнія коми кыв 2000: 163
+Ins:ӧн WordEnd ;
CAR_тӧг-THEN-K ;
ELA_ісь-THEN-K ;
+Ill:ӧ WordEnd ;
+Abl+PxPl2:янлысьныд WordEnd ;
+PxPl2+PxSg2+Acc2:янныдтӧ WordEnd ;
+PxPl2+Car:янныдтӧг WordEnd ;
+PxPl2+Cns:янныдла WordEnd ;
+Apr+PxPl2:янланьныд WordEnd ;
+Apr+PxSg2:янланьыд WordEnd ;
+Egr+PxPl2:янсяньныд WordEnd ;
+Ter:ӧдз WordEnd ;

LEXICON perssg3decl  !!≈ * __@CODE@__ 
+Nom:ія WordEnd ;
+Der/йӧ+Acc:ійӧ WordEnd ;

:ы GEN_лӧн-THEN-K ;
+Abl:ылісь WordEnd ;
:ы ELA_ісь-THEN-K ;
:ы DAT_лӧ-THEN-K ;
+Der/йӧ+Ins:ійӧн WordEnd ;
:ы perssg ;  ! for the regular obl sg
:ы WordEnd ;
!More form ӧния коми кыв 163
+Der/йӧ:ійӧ GEN_лӧн-THEN-K ;
+Der/йӧ+Abl:ійӧлісь WordEnd ;
+Der/йӧ:ійӧ DAT_лӧ-THEN-K ;
+Der/йӧ+Com:ійӧкӧт WordEnd ;
+Der/йӧ:ійӧ CAR_тӧг-THEN-K ;
+Der/йӧ:ійӧ APR_сянь-THEN-K ;
+Der/йӧ+Egr:ійӧсянь WordEnd ;
+Der/йӧ+Egr+PxSg3:ійӧсяньыс WordEnd ;
+Der/йӧ+Ter:ійӧӧдз WordEnd ;
! Special expressions
+Com+Ins:ыкӧтӧн WordEnd ; ! Ен сыкӧтӧн!

!! ми мийӧ The 1st and 2nd persons have Oblique case stem strategies
!! that differ from the 3rd person: 
!! ті тійӧ nämä ovat aivan eri asioita
!! сы сійӧ tosin joskus 

LEXICON perspl3decl !!≈ * __@CODE@__
+Nom:ія WordEnd ;
+Der/йӧ+Acc:ійӧ WordEnd ;

+Der/йӧ+Acc:айӧс WordEnd ;
:на GEN_лӧн-THEN-K ;
:ы persplobl ; !This might be one solution for the different stem types
:ы WordEnd ;
!
+Der/йӧ:найӧ GEN_лӧн-THEN-K ;
+Der/йӧ+Abl:найӧлісь WordEnd ;
+Der/йӧ:найӧ DAT_лӧ-THEN-K ;
+Der/йӧ+Ins:найӧӧн WordEnd ;
+Der/йӧ+Com:найӧкӧт WordEnd ;
+Com:накӧтыс WordEnd ;
+Der/йӧ:найӧ CAR_тӧг-THEN-K ;
+Car+PxSg3:натӧгыс WordEnd ;
+PxSg3+Cns:наысла WordEnd ;
+Ela+PxSg3:насьыс WordEnd ;
+PxSg3+Apr:наыслань WordEnd ;
+Apr+PxSg3:наланьыс WordEnd ;
+PxSg3+Egr:наыссянь WordEnd ;
+Egr+PxSg3:насяньыс WordEnd ;
+Tra+PxSg3:натіыс WordEnd ;
+Ter+PxSg3:наӧдзыс WordEnd ;


LEXICON perssg !!≈ * __@CODE@__
+Ins:ӧн WordEnd ;
+Com:кӧт WordEnd ;
CAR_тӧг-THEN-K ;
+Cns:ла WordEnd ;
+Ine:ын WordEnd ;
+Ill:ӧ WordEnd ; !
ELA_ісь-THEN-K ;
APR_сянь-THEN-K ;
+Egr:сянь WordEnd ;
+Prl:ӧт WordEnd ;
+TerI:ӧдзз WordEnd ;
+Abl:лісь WordEnd ; !This is not attested for Sg1 and Sg2 in the 2000 Grammar



LEXICON persplobl !!≈ * __@CODE@__
+Abl:лісь WordEnd ;
DAT_лӧ-THEN-K ;
+Acc:ӧс WordEnd ;

+Ins:ӧн WordEnd ;
+Com:кӧт WordEnd ;
CAR_тӧг-THEN-K ;
+Cns:ла WordEnd ;
+Cmpr:ся WordEnd ;
+Ine:ын WordEnd ;
ELA_ісь-THEN-K ;
+Ill:ӧ WordEnd ;
APR_сянь-THEN-K ;
+Egr:сянь WordEnd ;
+Prl:ӧд WordEnd ;
+Tra:ті WordEnd ;
+Ter:ӧдз WordEnd ;
+AprIne:ланьын WordEnd ;  ! New cases in last grammar  
+AprEla:ланьысь WordEnd ;  ! 
+AprIll:ланьӧ WordEnd ; ! 
+AprEgr:ланьсянь WordEnd ; ! 
+AprPrl:ланьӧд WordEnd ; !
+AprTra:ланьті WordEnd ; !       
+AprTer:ланьӧдз WordEnd ; ! 


! These ones to be removed:
! LEXICON PRON-PERS-PL1-ACC
! +Pron+Pers+Pl1+Acc: WordEnd ;
! LEXICON PRON-PERS-PL1-COM
! +Pron+Pers+Pl1+Com: WordEnd ;
! LEXICON PRON-PERS-PL1-DAT
! +Pron+Pers+Pl1+Dat: WordEnd ;
! LEXICON PRON-PERS-PL1-GEN
! +Pron+Pers+Pl1+Gen: WordEnd ;
! LEXICON PRON-PERS-PL1-NOM
! +Pron+Pers+Pl1+Nom: WordEnd ;
! LEXICON PRON-PERS-PL2-ACC
! +Pron+Pers+Pl2+Acc: WordEnd ;
! LEXICON PRON-PERS-PL2-COM
! +Pron+Pers+Pl2+Com: WordEnd ;
! LEXICON PRON-PERS-PL2-DAT
! +Pron+Pers+Pl2+Dat: WordEnd ;
! LEXICON PRON-PERS-PL2-GEN
! +Pron+Pers+Pl2+Gen: WordEnd ;
! LEXICON PRON-PERS-PL2-NOM
! +Pron+Pers+Pl2+Nom: WordEnd ;
! LEXICON PRON-PERS-PL3-ACC
! +Pron+Pers+Pl3+Acc: WordEnd ;
! LEXICON PRON-PERS-PL3-COM
! +Pron+Pers+Pl3+Com: WordEnd ;
! LEXICON PRON-PERS-PL3-DAT
! +Pron+Pers+Pl3+Dat: WordEnd ;
! LEXICON PRON-PERS-PL3-ELA
! +Pron+Pers+Pl3+Ela: WordEnd ;
! LEXICON PRON-PERS-PL3-GEN
! +Pron+Pers+Pl3+Gen: WordEnd ;
! LEXICON PRON-PERS-PL3-NOM
! +Pron+Pers+Pl3+Nom: WordEnd ;
! LEXICON PRON-PERS-SG1-ACC
! +Pron+Pers+Sg1+Acc: WordEnd ;
! LEXICON PRON-PERS-SG1-DAT
! +Pron+Pers+Sg1+Dat: WordEnd ;
! LEXICON PRON-PERS-SG1-ELA
! +Pron+Pers+Sg1+Ela: WordEnd ;
! LEXICON PRON-PERS-SG1-GEN
! +Pron+Pers+Sg1+Gen: WordEnd ;
! LEXICON PRON-PERS-SG1-NOM ! tähän tulee ме
! +Pron+Pers+Sg1+Nom: WordEnd ;
! LEXICON PRON-PERS-SG2-ACC
! +Pron+Pers+Sg2+Acc: WordEnd ;
! LEXICON PRON-PERS-SG2-COM
! +Pron+Pers+Sg2+Com: WordEnd ;
! LEXICON PRON-PERS-SG2-DAT
! +Pron+Pers+Sg2+Dat: WordEnd ;
! LEXICON PRON-PERS-SG2-ELA
! +Pron+Pers+Sg2+Ela: WordEnd ;
! LEXICON PRON-PERS-SG2-GEN
! +Pron+Pers+Sg2+Gen: WordEnd ;
! LEXICON PRON-PERS-SG2-NOM
! +Pron+Pers+Sg2+Nom: WordEnd ;
! LEXICON PRON-PERS-SG2-ABL
! +Pron+Pers+Sg2+Abl: WordEnd ;
! LEXICON PRON-PERS-SG3-ACC
! +Pron+Pers+Sg3+Acc: WordEnd ;
! LEXICON PRON-PERS-SG3-COM
! +Pron+Pers+Sg3+Com: WordEnd ;
! LEXICON PRON-PERS-SG3-DAT
! +Pron+Pers+Sg3+Dat: WordEnd ;
! LEXICON PRON-PERS-SG3-ELA
! +Pron+Pers+Sg3+Ela: WordEnd ;
! LEXICON PRON-PERS-SG3-GEN
! +Pron+Pers+Sg3+Gen: WordEnd ;
! LEXICON PRON-PERS-SG3-NOM
! +Pron+Pers+Sg3+Nom: WordEnd ;
! LEXICON PRON-PERS-SG3-ABL
! +Pron+Pers+Sg3+Abl: WordEnd ;
! LEXICON PRON-PERS-SG3-OBL
! +Pron+Pers+Sg3: DAT_лӧ-THEN-K ;
! +Pron+Pers+Sg3+Abl:%>лісь WordEnd ;


!Interrogative pronouns serve as the base of interrogative, relative, indefinite and distributional pronouns.

LEXICON INTERR
кин+Pron+Sem/Hum+Nom:кин INTERR-REL ; ! 2011-11-09 This will need cases
кодар+Pron+A:кодар INTERR-REL-INDEF ;
кодар+Pron+A+Der/MWN:кодар PRON-HUM-OBL ;
кодар+Pron+A+Der/ладор:кодарладор PRON-HUM-OBL ;

коді:код PRON-HUM ;

кодыр+Pron+Adv+Temp:кодыр INTERR-REL-INDEF ;
кор+Pron+Adv+Temp:кор INTERR-REL-INDEF ;
кор+Pron+Adv+Temp+Pl+Ill:коръясӧ INTERR-REL-INDEF ;
коръя+Pron+Adv+Temp:коръя INTERR-REL-INDEF ;
кудз+Pron+Adv+Manner+Err/Orth:кудз INTERR-REL-INDEF ;
кутшӧм+Pron+A:кутшӧм INTERR-REL-INDEF ; ! This needs to be 
кутшӧм+Pron+A+Pred+Pl:кутшӧмӧсь INTERR-REL-INDEF ; ! This needs to be 
кутшӧма+Pron+Adv+Manner:кутшӧма INTERR-REL-INDEF ; ! This needs to be 
кутшӧм+Pron+A+Der/MWN+N+Sg:кутшӧм PRON-NONHUM ;
кутшӧм+Pron+A+Der/MWN+N+Pl:кутшӧмъяс PRON-NONHUM ;
кутшӧми+Pron+A:кутшӧми INTERR-REL-INDEF ; ! This needs to be 
кыдз+Pron+Adv+Manner:кыдз INTERR-REL-INDEF ;
кыдзи+Pron+Adv+Manner:кыдзи INTERR-REL-INDEF ;
кыдзик+Pron+Adv+Manner:кыдзик INTERR-REL-INDEF ;
кыдзик+Pron+Adv+Manner+Ins:кыдзикӧн INTERR-REL-INDEF ;
кымын+Pron+Qnt:кымын INTERR-REL-INDEF ; ! Quantifier info required
кымын+Pron+Qnt+Iter:кымынысь INTERR-REL-INDEF ; ! Quantifier info required
кымын+Pron+Qnt+Ord:кымынӧд INTERR-REL-INDEF ; ! Quantifier info required
кымын+Pron+Qnt+Ord+Iter:кымынӧдысь INTERR-REL-INDEF ; ! Quantifier info required
кысь+Pron+Spat+Ela:кысь INTERR-REL-INDEF ;
кытчӧ+Pron+Spat+Ill:кытчӧ INTERR-REL-INDEF ;
кытчӧдз+Pron+Spat+Ter:кытчӧдз INTERR-REL-INDEF ;
кытысь+Pron+Spat+Ela:кытысь INTERR-REL-INDEF ;
кыті+Pron+Spat+Tra:кыті INTERR-REL-INDEF ;
кыткӧ+Pron+Spat+Tra+Der/кӧ+Indef:кыткӧ K ;
кытсюрӧ+Pron+Spat+Tra+Der/сюрӧ+Indef:кытсюрӧ K ;
кытӧн+Pron+Spat+Ine:кытӧн INTERR-REL-INDEF ;
кытӧні+Pron+Spat+Ine:кытӧні INTERR-REL ;
 
кӧні+Pron+Adv+Spat+Ine+Pred+Sg:кӧні INTERR-REL-INDEF ;
кӧні+Pron+Adv+Spat+Ine+Pred+Pl:кӧнӧсь INTERR-REL ;
кӧні+Pron+Adv+Spat+Ine:кӧн PRON-INDEFTAG ;
мый+Pron+NonHum:мый PRON-NONHUM ;
мыйда+Pron+NonHum:мыйда INTERR-REL-INDEF ;
мыйта+Pron+NonHum:мыйта INTERR-REL-INDEF ;
мыйтӧм+Pron+NonHum:мыйтӧм INTERR-REL-INDEF ;
мыйӧс+Pron+NonHum+Err/Orth:мыйӧс INTERR-REL-INDEF ;
!Here are some negative pronouns out of place 2011-11-22
некысь+Pron+Neg+Spat+Ela:некысь K ;
некысянь+Pron+Neg+Spat+Egr:некысянь K ;
некыт+Pron+Neg+Spat+Tra:некыт K ;
некытчӧ+Pron+Neg+Spat+Ill:некытчӧ K ;
некытчӧдз+Pron+Neg+Spat+Ter:некытчӧдз K ;
некытысь+Pron+Neg+Spat+Ela:некытысь K ;
некыті+Pron+Neg+Spat+Tra:некыті K ;
некытӧн+Pron+Neg+Spat+Ine:некытӧн K ;
некӧн+Pron+Neg+Spat+Ine:некӧн K ;



LEXICON PRON-HUM
+Pron+Sg+Nom:і INTERR-REL ;
+Pron+Pl+Nom:ъяс INTERR-REL-INDEF ;
+Pron+Sg+Nom: INTERR-REL-INDEF ;
+Pron+Sg: PRON-HUM-OBL ;
+Pron+Pl:ъяс PRON-HUM-OBL ;
+Pron: PRON-HUM-COLL ;
!код, коді

LEXICON PRON-HUM-OBL
! 2000: 187
+Cmpl: INTERR-REL-INDEF ;
+Gen:лӧн INTERR-REL-INDEF ;
+Abl:лісь INTERR-REL-INDEF ; !
+Dat:лӧ INTERR-REL-INDEF ;
+Acc:ӧс INTERR-REL-INDEF ;
+Acc3:сӧ INTERR-REL-INDEF ;
+Ins:ӧн INTERR-REL-INDEF ;
+Com:кӧт INTERR-REL-INDEF ;
+Car:тӧг INTERR-REL-INDEF ;
+Cns:ла INTERR-REL-INDEF ;
+Ine:ын INTERR-REL-INDEF ;
+Ela:ісь INTERR-REL-INDEF ; 
+Ill:ӧ INTERR-REL-INDEF ; !
+Apr:лань INTERR-REL-INDEF ;
+Egr:сянь INTERR-REL-INDEF ;
+Tra:ті INTERR-REL-INDEF ;
+Ter:ӧдз INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL
+Coll:нан PRON-HUM-COLL-PERS ;
+ZeroColl: PRON-HUM-COLL-PERS ;
!
+ZeroColl+PxPl2+Acc2:ныдтӧ INTERR-REL-INDEF ;
+Coll+PxPl2+Acc2:наннытӧ INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL-PERS
+PxPl1:ным PRON-HUM-COLL-CASES ;
+PxPl2:ныд PRON-HUM-COLL-CASES ;
+PxPl3:ныс PRON-HUM-COLL-CASES ;
!
+PxPl1+Acc:нымӧс INTERR-REL-INDEF ;
+PxPl2+Acc2:нытӧ INTERR-REL-INDEF ;
+PxPl3+Acc3:ныссӧ INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL-CASES
+Nom: INTERR-REL-INDEF ;
 PRON-HUM-OBL ;

LEXICON PRON-NONHUM
+Nom: INTERR-REL-INDEF ;
+Gen:лӧн INTERR-REL-INDEF ; !
+Abl:лісь INTERR-REL-INDEF ; !
+Dat:лӧ INTERR-REL-INDEF ; !
+ZeroAcc: INTERR-REL-INDEF ;
+Acc3:сӧ INTERR-REL-INDEF ;
+Ins:ӧн INTERR-REL-INDEF ;
+Com:кӧт INTERR-REL-INDEF ;
+Car:тӧг INTERR-REL-INDEF ;
+Cmpr:ся INTERR-REL-INDEF ;
+Cns:ла INTERR-REL-INDEF ;
+Ine:ын INTERR-REL-INDEF ;
+Ela:ісь INTERR-REL-INDEF ; 
+Ill:ӧ INTERR-REL-INDEF ; !
+Apr:лань INTERR-REL-INDEF ;
+Egr:сянь INTERR-REL-INDEF ;
+Prl:ӧд INTERR-REL-INDEF ;
+Tra:ті INTERR-REL-INDEF ;
+Ter:ӧдз INTERR-REL-INDEF ;

! Removing 2012-02-08 The comparative can be taken care of once
! LEXICON INTERR-REL-INDEF_COMPARISON
! +Der/джык+Comp:%>джык INTERR-REL ;
!               INTERR-REL ;

LEXICON Ill4INTERR-REL-INDEF
+Ill: INTERR-REL-INDEF ;

LEXICON EgrIll4INTERR-REL-INDEF
+Egr+Ill: INTERR-REL-INDEF ;

LEXICON Egr4INTERR-REL-INDEF
+Egr: INTERR-REL-INDEF ;

LEXICON Tra4INTERR-REL-INDEF
+Pron+Tra: INTERR-REL-INDEF ;

LEXICON TraEgr4INTERR-REL-INDEF
+Tra+Egr: INTERR-REL-INDEF ;

LEXICON Ter4INTERR-REL-INDEF
+Ter: INTERR-REL-INDEF ;

LEXICON INTERR-REL-INDEF ! 3 types of pronouns are congregated here
 PRON-INDEFTAG ; ! This leads to a variety of indefinite pronouns
 INTERR-REL ; ! This leads to a split between relative and interrogative pronouns

LEXICON INTERR-REL
+Interr: COMPARATIVE ;
+Rel: K ;

LEXICON PRON-INDEFTAG
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ;


! Indefinite pronouns
 
LEXICON PRONINDEFTAG
+Pron+Indef: WordEnd ;

LEXICON PRONINDEFTAG-KO
+Pron: PRONINDEF-PERS-KO-CX ;

LEXICON PRONINDEF-PERS-KO-CX
+Nom: PRONINDEF-PERS-KO-END ;
+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
+Abl:%>лісь PRONINDEF-PERS-KO-END ;
+Dat:%>лӧ PRONINDEF-PERS-KO-END ;
+Ins:%>ӧн PRONINDEF-PERS-KO-END ;
+Ela:%>ісь PRONINDEF-PERS-KO-END ; !2011-10-19 It might be that this only has to do with мый _what_ and мыйыськӧ _for some reason_
+Com:%>кӧт PRONINDEF-PERS-KO-END ;
+Egr:%>сянь PRONINDEF-PERS-KO-END ;

LEXICON PRONINDEF-PERS-KO-END
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ; !Basically both endings can be used for deriving 
 !indefinite pronouns from the interrogative/relative pronouns.


LEXICON IS_PRON-INDEF-KO
+Pron+Der/кӧ+Indef: K ;

LEXICON IS_PRON-INDEF-SJURO
+Pron+Der/сюрӧ+Indef: K ;

LEXICON IS_PRON-INTERR
+Pron+Interr: K ;

!LEXICON PRONINDEF-PERS-END
!+Der/сюрӧ:%>сюрӧ WordEnd ;

!LEXICON PRONINDEF-PERS-KO
!+Nom: PRONINDEF-PERS-KO-END ;
!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
!+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
!+Dat:%>лӧ PRONINDEF-PERS-KO-END ;
!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;


LEXICON PRON-SPAT
+Pron+Spat: COMPARATIVE ;

LEXICON PRON-RECIPR
+Pron+Recipr: PRON-RECIPR-CASE ;

LEXICON PRON-RECIPR-CASE
+Cmpl: WordEnd ;
+PxSg2+Cmpl:ыд K ;
+PxSg2+Cmpl:ыс K ;
+PxPl1+Cmpl:ным K ;
+PxPl2+Cmpl:ныд K ;
+PxPl3+Cmpl:ныс K ;
GEN_лӧн-THEN-K ;
+PxSg3+Abl:ыслісь K ;
+PxPl3+Abl:ныслісь K ;
DAT_лӧ-THEN-K ;
+PxSg3+Dat:ыслӧ K ;
+PxPl3+Dat:ныслӧ K ;
+Acc:ӧс K ;
+PxPl1+Acc:нымӧс K ;
+PxPl2+Acc2:нытӧ K ;
+PxPl3+Acc3:ныссӧ K ;
+Acc3:сӧ K ;
+PxSg3+Com:ыскӧт K ;
+PxPl2+Com:ныдкӧт K ;
+PxPl3+Com:ныскӧт K ;
+Car+PxSg3:тӧгыс K ;
ELA_ісь-THEN-K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl3:сьыныс K ;
+PxSg3+Ill:ас K ; ! Here is a good problem: Should there be a special tag
                ! for the illative-inessive forms
+PxPl3+Ill:нысӧ K ;


LEXICON PRON-REL
+Pron+Rel: WordEnd ;

LEXICON PRON-REFL-IN_LADOR
+Pron+Refl+Der/ладор: Noun1 ; !2011-11-22

LEXICON EndRefl
+Pron+Refl+Abs: WordEnd ; !ас is an absolute-form modifer
 PRON-REFL-OBL-CASES ;

LEXICON PRON-REFL-OBL-CASES
+Ela+PxSg1:сим K ;
+Ela+PxSg2:сит K ;
+Ela+PxSg3:сис K ;
+Ela+PxPl1:синым K ;
+Ela+PxPl1+Abl:синымлісь K ;
+Ela+PxPl1+Ela:синымысь K ;
+Ela+PxPl2:синыт K ;
+Ela+PxPl3:синыс K ;
+Dat+PxSg1:лым K ;
+Dat+PxSg2:лыт K ;
+Dat+PxSg3:лыс K ;
+Dat+PxPl1:лыным K ;
+Dat+PxPl2:лыныт K ;
+Dat+PxPl2+Dat:лынытлӧ K ;
+Dat+PxPl3:лыныс K ;
+Dat+PxPl3+Dat:лыныслӧ K ;
+PxPl3+Dat:ныслӧ K ;
+Ins+PxSg1:ӧн K ; !There is no indication of person. = Sg1, Pl3
+Ins+PxPl3:ӧн K ; !There is no indication of person. = Sg1, Pl3
+Ins+PxSg1+Ins:намӧн K ;
+Ins+PxSg2:над K ;
+Ins+PxSg3:нас K ;
+Ins+PxPl1:наным K ;
+Ins+PxPl2:наныд K ;
+Ins+PxPl3:наныс K ;
+Com+PxSg1:кӧт K ; !There is no indication of person. = Sg1
+Com+PxSg2:кӧтыд K ; !
+Com+PxSg3:кӧтыс K ; !
+Com+PxPl1:кӧтным K ; !
+Com+PxPl2:кӧтныд K ; !
+Com+PxPl2:ныдкӧт K ; !
+Com+PxPl3:кӧтныс K ; !
+Com+PxPl3:ныскӧт K ; !
+Car+PxSg1:тӧг K ; !There is no indication of person. = Sg1
+Car+PxSg2:тӧгыд K ; !
+Car+PxSg3:тӧгыс K ; !
+Car+PxPl1:тӧгным K ; !
+Car+PxPl2:тӧгныд K ; !
+Car+PxPl2:ныдтӧг K ; !
+Car+PxPl3:тӧгныс K ; !
+Car+PxPl3:ныстӧг K ; !
+Cons+PxSg1:ла K ; !There is no indication of person. = Sg1
+Cons+PxSg2:лаыд K ;
+Cons+PxSg2:ыдла K ;
+Cons+PxSg3:ысла K ;
+Cons+PxPl2:ныдла K ;
+Cons+PxPl3:нысла K ;
+Ine+PxSg2:ад K ;
+Ine+PxSg3:ас K ;
+Ine+PxPl1:аным K ;
+Ine+PxPl2:аныд K ;
+Ine+PxPl3:аныс K ;
+Ill+PxSg1:ӧ K ; !There is no indication of person. = Sg1
+Ill+PxSg1:ам K ; !Ӧнія коми кыв gives this for illative only
+Ill+PxSg2:ад K ; !
+Ill+PxSg3:ас K ; !
+Ill+PxPl1:аным K ; !
+Ill+PxPl2:аныд K ; !
+Ill+PxPl3:аныс K ; !
+Apr+PxSg1:лань K ; !There is no indication of person. = Sg1
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxPl1:ланьным K ;
+Apr+PxPl2:ланьныд K ;
+Apr+PxPl3:ланьныс K ;
+Sg1+Egr:сянь K ; !There is no indication of person. = Sg1
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Egr+PxPl1:сяньным K ;
+Egr+PxPl2:сяньныд K ;
+Egr+PxPl3:сяньныс K ;
+Sg1+Tra:ті K ; !There is no indication of person. = Sg1
+Tra+PxSg2:тіыд K ;
+Tra+PxSg3:тіыс K ;
! Pl1 and Pl2 are not attested in Ӧнія коми кыв
+Tra+PxPl3:тіныс K ;
+Sg1+Ter:ӧдз K ; !There is no indication of person. = Sg1
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;
+PxPl1+Ter:нымӧдз K ;
+PxPl2+Ter:ныдӧдз K ;
+PxPl3+Ter:нысӧдз K ;
!Extras 2011-10-07


LEXICON PRON-REFL-SG1-NOM
+Pron+Refl+Sg1+PxSg1+Nom: WordEnd ;
+Pron+Refl+Sg1+PxSg1+Acc:ӧс WordEnd ;
+Pron+Refl+Sg1+PxSg1+Ins:ӧн WordEnd ;
+Pron+Refl+Sg1+PxSg1+Com:кӧт WordEnd ;
+Pron+Refl+Sg1+PxSg1+Car:тӧг WordEnd ;
+Pron+Refl+Sg1+PxSg1+Ine:ын WordEnd ;
+Pron+Refl+Sg1+PxSg1+Ela:ысь WordEnd ;
+Pron+Refl+Sg1+PxSg1+Ill:ӧ WordEnd ;


LEXICON PRON-REFL-SG1-GEN
+Pron+Refl+Gen+PxSg1: WordEnd ;
+Pron+Refl+Gen+PxSg1+Com:кӧт WordEnd ;
+Pron+Refl+Gen+PxSg1+Egr:сянь WordEnd ;

LEXICON PRON-REFL-SG2-NOM
+Pron+Refl+Nom+PxSg2: WordEnd ;
+Pron+Refl+Nom+PxSg2+Car:тӧг WordEnd ;
+Pron+Refl+Nom+PxSg2+Ela:ысь WordEnd ;

LEXICON PRON-REFL-SG2-GEN
+Pron+Refl+Gen+PxSg2: WordEnd ;
+Pron+Refl+Gen+PxSg2+Erg:сянь WordEnd ;

LEXICON PRON-REFL-SG2-ACC
+Pron+Refl+Sg2+Acc2: WordEnd ;

LEXICON PRON-REFL-SG3-NOM
+Pron+Refl+Sg3+Nom: WordEnd ;
+Pron+Refl+Sg2+PxSg2+Ins:ӧн WordEnd ;
+Pron+Refl+Sg2+PxSg2+Com:кӧт WordEnd ;
+Pron+Refl+Sg2+PxSg2+Car:тӧг WordEnd ;
+Pron+Refl+Sg1+PxSg2+Ela:ысь WordEnd ;

LEXICON PRON-REFL-SG3-GEN
+Pron+Refl+Gen+PxSg3: WordEnd ;
+Pron+Refl+Gen+PxSg3+Gen:лӧн WordEnd ;
+Pron+Refl+Gen+PxSg3+Ine:ӧ WordEnd ;

LEXICON PRON-REFL-SG3-ACC
+Pron+Refl+Sg3+Acc3: WordEnd ;

LEXICON PRON-REFL-PL1-NOM
+Pron+Refl+Nom+PxPl1: WordEnd ;
+Pron+Refl+PxPl1+Acc:ӧс WordEnd ;
+Pron+Refl+PxPl1+Com:кӧт WordEnd ;
+Pron+Refl+PxPl1+Car:тӧг WordEnd ;
+Pron+Refl+PxPl1+Ela:ысь WordEnd ;
+Pron+Refl+PxPl1+Ter:ӧдз WordEnd ;

LEXICON PRON-REFL-PL1-GEN
+Pron+Refl+Gen+PxPl1: WordEnd ;
+Pron+Refl+Gen+PxPl1+Abl:лісь WordEnd ;
+Pron+Refl+Gen+PxPl1+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL2-NOM
+Pron+Refl+Nom+PxPl2: WordEnd ;
+Pron+Refl+Nom+PxPl2+Acc2:тӧ WordEnd ;
+Pron+Refl+Nom+PxPl2+Cns:ла WordEnd ;
+Pron+Refl+Nom+PxPl2+Car:тӧг WordEnd ;
+Pron+Refl+Nom+PxPl2+Ela:ысь WordEnd ;

LEXICON PRON-REFL-PL2-GEN
+Pron+Refl+Gen+PxPl2: WordEnd ;
+Pron+Refl+Gen+PxPl2+Gen: WordEnd ;
+Pron+Refl+Gen+PxPl2+Der/ладор:%>ладор Noun1 ;


LEXICON PRON-REFL-PL2-ACC
+Pron+Refl+Pl2+Acc2: WordEnd ;

LEXICON PRON-REFL-PL3-NOM
+Pron+Refl+Nom+PxPl3: WordEnd ;
+Pron+Refl+Nom+PxPl3+Cns:ла WordEnd ;

LEXICON PRON-REFL-PL3-GEN
+Pron+Refl+Gen+PxPl3: WordEnd ;
+Pron+Refl+Gen+PxPl3+Gen:лӧн WordEnd ;
+Pron+Refl+Gen+PxPl3+Prl:ӧд WordEnd ;
+Pron+Refl+Gen+PxPl3+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL3-ACC
+Pron+Refl+Pl3+Acc3: WordEnd ;


LEXICON PRONDEMTAG
+Pron+Dem: WordEnd ; ! sg/pl?

LEXICON ADNDEMTAG
+Adn+Dem: K ;
+Adn+Dem+Pred+Pl:%>ӧсь K ;
+Adn+Dem+Der/MWN: Noun1 ;

LEXICON PRONDEMTAG-ES
+Pron+Dem+Sg+Nom:%>ійӧ K ;
+Pron+Dem+Sg+Acc:%>ійӧс K ;
+Pron+Dem+Sg+Ins:%>ійӧн K ;
+Pron+Dem+Sg:%>ы PRONDEMTAGSG ;
+Pron+Dem+Pl:%>ійӧяс WordEnd ;
+Pron+Dem+Pl:%>ійяс WordEnd ;
+Pron+Dem+Pl:%>ійӧян WordEnd ;
+Pron+Dem+Pl:%>ійян WordEnd ;

LEXICON PRONDEMTAGSG-TA
+Pron+Dem+Sg+Nom:%>йӧ K ;
+Pron+Dem+Sg+Acc:%>йӧс K ;
+Pron+Dem+Sg+Ins:%>йӧн K ;
+Pron+Dem+Sg: PRONDEMTAGSG ;
+Pron+Dem+Pl:%>йӧяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йӧян PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йян PRONDEMTAGPL ;

LEXICON PRONDEMTAGSG 
+Cmpl: WordEnd ;
+Ins:ӧн K ;
+Com:кӧт K ;
CAR_тӧг-THEN-K ;
+Cns:ла K ;
+Ine:ын K ;
ELA_ісь-THEN-K ;
+Ill:ӧ K ; !This is questioned in the 2000 Grammar
+Abl:лісь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;


LEXICON PRONDEMTAGPL
!+Pron+Dem+Pl: WordEnd ;
+Nom: WordEnd ;
GEN_лӧн-THEN-K ;
+Acc:ӧс WordEnd ;
+Ins:ӧн K ;
+Com:кӧт K ;
CAR_тӧг-THEN-K ;
+Cns:ла K ;
+Ine:ын K ;
ELA_ісь-THEN-K ;
+Ill:ӧ K ; !
+Abl:лісь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-APR-EGR ;
 PRONDEMTAG-APR-ELA ;
 PRONDEMTAG-APR-ILL ;
 PRONDEMTAG-APR-INE ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;

LEXICON PRONDEMTAG-ELA
ELA_ісь-THEN-K ;

LEXICON PRONDEMTAG-APR
APR_сянь-THEN-K ;

LEXICON PRONDEMTAG-APR-EGR
+AprEgr:ланьсянь K ;

LEXICON PRONDEMTAG-APR-ELA
+AprEla:ланьысь K ;

LEXICON PRONDEMTAG-APR-ILL
+AprIll:ланьӧ K ;

LEXICON PRONDEMTAG-APR-INE
+AprIne:ланьын K ;

LEXICON PRONDEMTAG-EGR
+Egr:сянь K ;

LEXICON PRONDEMTAG-PRL
+Prl:ӧд K ;

LEXICON PRONDEMTAG-TRA
+Tra:ті K ;

LEXICON PRONDEMTAG-TER
+Ter:ӧдз K ;

! Word forms that already have their morphology 
LEXICON PRON-DEM-IS_INE
+Pron+Dem+Ine: K ;

LEXICON PRON-DEM-IS_ILL
+Pron+Dem+Ill: K ;



LEXICON PRON-INTER-NONHUM
+Pron+Interr+Nom: WordEnd ;
+Pron+Interr: GEN_лӧн-THEN-K ;
+Pron+Interr+Abl:лісь K ; !
+Pron+Interr: DAT_лӧ-THEN-K ;
+Pron+Interr+ZeroAcc: WordEnd ;
+Pron+Interr+Acc3:сӧ WordEnd ;
+Pron+Interr+Ins:ӧн K ;
+Pron+Interr+Com:кӧт K ;
+Pron+Interr: CAR_тӧг-THEN-K ;
+Pron+Interr+Cns:ла K ;
+Pron+Interr+Ine:ын K ;
+Pron+Interr: ELA_ісь-THEN-K ; 
+Pron+Interr+Ill:ӧ K ; !
+Pron+Interr: APR_сянь-THEN-K ;
+Pron+Interr+Egr:сянь K ;
+Pron+Interr+Prl:ӧд K ;
+Pron+Interr+Tra:ті K ;
+Pron+Interr+Ter:ӧдз K ;

LEXICON PRON-NEG-HUM
+Pron+Neg+Sg+Nom: WordEnd ;
+Pron+Neg+Sg: PRON-NEG-OBL ;

LEXICON PRON-DUAL
+Pron+Du: PRON-NEG-OBL ;

! LEXICON PRON-INTER-REL-HUM-COLL
! +Coll:нан PRON-INTER-REL-HUM-COLL-PERS ;
! +ZeroColl: PRON-INTER-REL-HUM-COLL-PERS ;
! +ZeroColl+PxPl2+Acc2:ныдтӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Coll+PxPl2+Acc2:наннытӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-PERS
! +PxPl1:ным PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl2:ныд PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl3:ныс PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl1+Acc:нымӧс PRON-INTER-REL-HUM-COLL-INDEF ;
! +PxPl3+Acc3:ныссӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-CASES
! +Nom: PRON-INTER-REL-HUM-COLL-INDEF ;
! +Gen:лӧн PRON-INTER-REL-HUM-COLL-INDEF ;
! +Abl:лісь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Dat:лӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ins:ӧн PRON-INTER-REL-HUM-COLL-INDEF ;
! +Com:кӧт PRON-INTER-REL-HUM-COLL-INDEF ;
! +Car:тӧг PRON-INTER-REL-HUM-COLL-INDEF ;
! +Cns:ла PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ine:ын PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ela:ысь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ill:ӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Apr:лань PRON-INTER-REL-HUM-COLL-INDEF ;
! +Egr:сянь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Tra:ті PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ter:ӧдз PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-INDEF
! +Der/кӧ+Indef:кӧ K ;
!  K ;

!Actually there might be reason to consider +Der/MWN
LEXICON PRONQ-MUKOD
+Pron+Qnt+Abs: WordEnd ;
+Pron+Qnt+Sg+Nom: K ;
+Pron+Qnt+Sg: PRONQ-STAV-CASES ;
+Pron+Qnt+Pl+Nom:ъяс K ;
+Pron+Qnt+Pl:ъяс PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV
+Pron+Qnt+Abs: WordEnd ;
+Pron+Qnt+Sg+Nom: K ;
+Pron+Qnt+Sg+ZeroAcc: K ;
+Pron+Qnt+Sg: PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV-CASES
+Nom+PxSg2:ыд K ;
+Nom+PxSg3:ыс K ;
+Nom+PxPl1:ным K ;
+Nom+PxPl2:ныд K ;
+Nom+PxPl3:ныс K ;

+Ins:ӧн K ;
+Ins+PxSg1:нам K ;
+Ins+PxSg2:над K ;
+Ins+PxSg3:нас K ;
+PxPl1+Ins:нымӧн K ; ! Should there be a Der/ӧн tag?
+PxPl2+Ins:ныдӧн K ; ! Should there be a Der/ӧн tag?
+PxPl3+Ins:нысӧн K ; ! Should there be a Der/ӧн tag?

ELA_ісь-THEN-K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl1:сьыным K ;
+PxPl1+Ela:нымысь K ;
+Ela+PxPl2:сьыныд K ;
+PxPl2+Ela:ныдысь K ;
+Ela+PxPl3:сьыныс K ;
+PxPl3+Ela:нысысь K ;

GEN_лӧн-THEN-K ;
+PxSg3+Gen:ыслӧн K ;
+PxPl1+Gen:нымлӧн K ;
+PxPl2+Gen:ныдлӧн K ;
+PxPl3+Gen:ныслӧн K ;

+Acc:ӧс K ;
+PxSg1+Acc:ӧс K ;
+PxSg2+Acc2:тӧ K ;
+PxSg3+Acc3:сӧ K ;
+PxPl1+Acc:нымӧс K ;
+PxPl2+Acc2:нытӧ K ;
+PxPl3+Acc3:нысӧ K ;

+PxSg2+Dat:ыдлӧ K ;
+PxSg3+Dat:ыслӧ K ;
+PxPl1+Dat:нымлӧ K ;
+PxPl2+Dat:ныдлӧ K ;
+PxPl3+Dat:ныслӧ K ;

+Abl:лісь K ;
+PxSg3+Abl:ыслісь K ;
+PxPl1+Abl:нымлісь K ;
+PxPl2+Abl:ныдлісь K ;
+PxPl3+Abl:ныслісь K ;

+Com:кӧт K ;
+PxSg3+Com:ыскӧт K ;
+PxPl1+Com:нымкӧт K ;
+PxPl2+Com:ныдкӧт K ;
+PxPl3+Com:ныскӧт K ;

CAR_тӧг-THEN-K ;
+PxSg3+Car:ыстӧг K ;
+PxPl1+Car:нымтӧг K ;
+PxPl2+Car:ныдтӧг K ;
+PxPl3+Car:ныстӧг K ;

+Cns:ла K ;
+PxSg3+Cns:ысла K ;
+PxPl1+Cns:нымла K ;
+PxPl2+Cns:ныдла K ;
+PxPl3+Cns:нысла K ;

+Cmpr+PxSg3:сяыс K ;

+Ine:ын K ;
+PxPl1+Ine:нымын K ;
+PxPl2+Ine:ныдын K ;
+PxPl3+Ine:нысын K ;

+Ill:ӧ K ;
+PxPl1+Ill:нымӧ K ;
+PxPl2+Ill:ныдӧ K ;
+PxPl3+Ill:нысӧ K ;

+Egr:сянь K ;
+Egr+PxSg3:сяньыс K ;
+PxSg3+Egr:ыссянь K ;
+Egr+PxPl1:сяньным K ;
+PxPl1+Egr:нымсянь K ;
+Egr+PxPl2:сяньныд K ;
+PxPl2+Egr:ныдсянь K ;
+PxPl3+Egr:ныссянь K ;

APR_сянь-THEN-K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxSg3:ыслань K ;
+Apr+PxPl1:нымлань K ;
+Apr+PxPl2:ныдлань K ;
+Apr+PxPl3:ныслань K ;

+Tra:ті K ;
+Tra+PxSg3:тіыс K ;
+Tra+PxSg3:ысті K ;
+Tra+PxPl1:нымті K ;
+Tra+PxPl2:ныдті K ;
+Tra+PxPl3:нысті K ;

+Ter:ӧдз K ;
+PxSg3+Ter:ысӧдз K ;
+PxPl1+Ter:нымӧдз K ;
+PxPl2+Ter:ныдӧдз K ;
+PxPl3+Ter:нысӧдз K ;



! COMPARATIVE ; ! sg/pl?

LEXICON PRONQ-COLL
+Pron+Qnt+Coll+Sg: PRONQ-COLL-CASE ; ! sg/pl?

! быдӧн, ӧтнасӧн, ӧтнамӧн, ӧтнадӧн, ставӧн, кымынӧн, унаӧн, кыкӧн

LEXICON PRONQ-COLL-CASE
!ӧнія коми кыв 2000: 183
+Nom: K ;
GEN_лӧн-THEN-K ;
+PxSg2+Gen:ыдлӧн K ;
+PxSg3+Gen:ыслӧн K ;
+Abl:лісь K ;
+PxSg2+Abl:ыдлісь K ;
+PxSg3+Abl:ыслісь K ;
DAT_лӧ-THEN-K ;
+PxSg2+Dat:ыдлісь K ;
+PxSg3+Dat:ыслісь K ;
+Acc:ӧс K ;
+PxSg2+Acc2:тӧ K ;
+PxSg3+Acc3:сӧ K ;
+Ins:ӧн K ;
+Com:кӧт K ;
+Com+PxSg2:ыдкӧт K ;
+Com+PxSg3:ыскӧт K ;
CAR_тӧг-THEN-K ;
+Car+PxSg2:тӧгыд K ;
+Car+PxSg3:тӧгыс K ;
+Cns:ла K ;
+Cns+PxSg2:ыдла K ;
+Cns+PxSg3:ысла K ;
+Ine:ын K ;
ELA_ісь-THEN-K ;
+Ela+PxSg2:сьыд K ;
+Ela+PxSg3:сьыс K ;
+Ill:ӧ K ;
APR_сянь-THEN-K ;
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Egr:сянь K ;
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Tra:ті K ;
+Ter:ӧдз K ;
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;

LEXICON PRONQ-ATAG
+Pron+Qnt+A: K ;
+Pron+Qnt+A+Der/MWN: Noun1 ;

LEXICON PRON-QNT_
+Pron+Qnt: COMPARATIVE ; ! sg/pl?

LEXICON PRON-QNT_-PL
+Pron+Qnt+Pl: WordEnd ;

LEXICON PRON-QNT-IS_ILL
+Pron+Qnt+Ill: K ;


LEXICON PRON-NEG-End
+Pron+Neg: K ;

LEXICON PRON-NEG-A
+Pron+Neg+A: WordEnd ;
+Pron+Neg+A+Der/MWN+N+Sg+Nom: WordEnd ;
+Pron+Neg+A+Der/MWN+N+Sg: PRON-NEG-OBL ;

LEXICON PRON-NEG-ADV
+Pron+Neg+Adv: COMPARATIVE ;

LEXICON PRON-NEG-IN_LA
+Pron+Neg+Der/ла+Spat: PRON-NEG-SPAT ;

LEXICON PRON-NEG-ITER
+Pron+Neg+Der/ысь+Iter: K ;

LEXICON PRON-NEG
+Pron+Neg+Sg+Nom: WordEnd ;
+Pron+Neg+Sg: PRON-NEG-OBL ;


LEXICON PRON-NEG-COLL
+Coll:нан PRON-NEG-COLL-PERS ;
+ZeroColl: PRON-NEG-COLL-PERS ;
!
+ZeroColl+PxPl2+Acc2:ныдтӧ K ;
+Coll+PxPl2+Acc2:наннытӧ K ;

LEXICON PRON-NEG-COLL-PERS
+PxPl1:ным PRON-NEG-COLL-CASES ;
+PxPl2:ныд PRON-NEG-COLL-CASES ;
+PxPl3:ныс PRON-NEG-COLL-CASES ;
!
+PxPl1+Acc:нымӧс K ;
+PxPl2+Acc2:нытӧ K ;
+PxPl3+Acc3:ныссӧ K ;

LEXICON PRON-NEG-COLL-CASES
+Nom: K ;
 PRON-NEG-OBL ;

LEXICON PRON-NEG-OBL
! 2011-11-15 This is used by PRON-NEG
! also тэа-мэа inclusive 1Du, сыа-мэа exclusive 1Du
+Cmpl: WordEnd ;
GEN_лӧн-THEN-K ;
+Abl:лісь K ; !
DAT_лӧ-THEN-K ;
+Acc:ӧс WordEnd ;
+Acc3:сӧ WordEnd ;
+Ins:ӧн K ;
+Com:кӧт K ;
CAR_тӧг-THEN-K ;
+Cns:ла K ;
 PRON-NEG-SPAT ;

LEXICON PRON-NEG-SPAT
+Ine:ын K ;
ELA_ісь-THEN-K ;
+Ill:ӧ K ; !
APR_сянь-THEN-K ;
+Egr:сянь K ;
+Tra:ті K ;
+Ter:ӧдз K ;
 
LEXICON PRON-NEG-IS_ILL
+Pron+Neg+Ill: K ;

!! Tagged in the src/morphology/stems/pronouns.xml file
LEXICON PRON_
+Pron: K ;
LEXICON PronInterr
+Pron+Interr: WordEnd ;
LEXICON PronIndef
+Pron+Indef: WordEnd ;

! vim: set ft=xfst-lexc:
