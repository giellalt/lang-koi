

! ====================================== !
!! !!!Verbal morphology               
! ====================================== !

LEXICON Verbs
verb    V_   ;
verbs ; 

LEXICON V_
+WORK: # ;

!! !!Closed class verbs
! ------------------

LEXICON VERBNEGATIVE
!Present Neg
оз+V+Neg+Ind+Prs+Sg1:ог MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg2:он MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg3:оз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:онӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧй MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:онӧй MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl3:озӧ MatrixVerb-Comparison ;


!Past Neg
оз+V+Neg+Ind+Prt+Sg1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Sg2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Sg3:эз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl1:эгӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl2:энӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl3:эзӧ MatrixVerb-Comparison ;

!Imperative
оз+V+Neg+Imprt+Sg2:эн K ;
оз+V+Neg+Imprt+Pl2:эн K ;
оз+V+Neg+Imprt+Pl2:энӧ K ;
!A tag is needed to indicate this clitic-like лы segment
оз+V+Neg+Imprt+Sg2:энлы K ;

!Final
моз+V+Neg+Final+1:мог K ; ! мед ог _so that I/we won't_
моз+V+Neg+Final+2:мон K ; ! мед он
моз+V+Neg+Final+3:моз K ; ! мед оз




!! !!Open class verbs
! ----------------

!! Some Flag diacritic lines are with regexes, other with aligned zeros. We want to migrate to
!! regexes < … > , for readability reasons (sic!)

LEXICON V_LOKNY !!≈ * __@CODE@__ One verb only, локны:лок
@U.CONJ-INF.YES@+V+Inf:@U.CONJ-INF.YES@%>ны VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.1@+V+Inf+Px1:@U.CONJ-INF.YES@@U.CONJ-POSS.1@%>ным VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2@+V+Inf+Px2:@U.CONJ-INF.YES@@U.CONJ-POSS.2@%>ныт VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3@+V+Inf+Px3:@U.CONJ-INF.YES@@U.CONJ-POSS.3@%>ныс VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+V+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+V+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысӧ VerbEnd-Comparison ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+V+ConNeg+Pl3:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@+V+ConNeg:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@ VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@+V+Imprt+Sg2:@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@ VerbEnd ;
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@%>т VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@%>т VerbDer-V ; !
+V:т  VerbConj_KUTNY ; !а


LEXICON VR_SHUNY !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj ; !а
+V+Refl: VerbConj-V ; !б

LEXICON V_SHUNY !!≈ * __@CODE@__ шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer-V ; !
+V: VerbConj ; !а
+V: VerbConj-V ; !б

LEXICON V_JUAYSHNY !!≈ * __@CODE@__ is there ш : шт gradation
 V_AMNY ; 
LEXICON VR_AMNY !!≈ * __@CODE@__
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj_AMNY ; !а
+V+Refl: VerbConj-V ; !б

LEXICON V_AMNY !!≈ * __@CODE@__ амны:ам Ending in -мны, -жны, -дзны,
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer-V ; !
+V: VerbConj_AMNY ; !а
+V: VerbConj-V ; !б

LEXICON VR_KYJNY !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-Y ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-JA ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_KYJNY ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj_KYJNY ; !а
+V+Refl:й VerbConj-V ; !б

LEXICON V_KYJNY !!≈ * __@CODE@__ кыйны:кы Ending in -йны, 
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-Y ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-JA ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer_KYJNY ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@й VerbDer-V ; !
+V: VerbConj_KYJNY ; !а
+V:й VerbConj-V ; !б

LEXICON V_CHILOESNY !!≈ * __@CODE@__ is there s : st alternation 2018-09-01
 V_KUTNY ;

LEXICON VR_KUTNY !!≈ * __@CODE@__
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj_KUTNY ; !а
+V+Refl: VerbConj-V ; !б

LEXICON V_KUTNY !!≈ * __@CODE@__ шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer-V ; !
+V: VerbConj_KUTNY ; !а
+V: VerbConj-V ; !б

LEXICON VR_NUAVNY !!≈ * __@CODE@__ нуавны:нуа
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@в Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@л Verb-nyDer-ONSET-VOW ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@л VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@в VerbDer-V ; !
+V+Refl:л VerbConj_KUTNY ; !а
+V+Refl:в VerbConj-V ; !б

LEXICON V_NUAVNY !!≈ * __@CODE@__ нуавны:нуа
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@в Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@л Verb-nyDer-ONSET-VOW ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@л VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@в VerbDer-V ; !
+V:л VerbConj_KUTNY ; !а
+V:в VerbConj-V ; !! Consonant-onset

LEXICON VR_ARTASJNY !!≈ * __@CODE@__
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ь Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ь Verb-nyDer-ONSET-Y ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-JA ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_ARTASJNY ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ь VerbDer-V ; !
+V+Refl: VerbConj ; !а
+V+Refl: VerbConj-V ; !б

LEXICON V_ARTASJNY !!≈ * __@CODE@__ артасьны:артас -Cны.
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ь Verb-nyDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer_ARTASJNY ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ь VerbDer-V ; !
+V: VerbConj ; !а
+V: VerbConj-V ; !б

LEXICON VR_LYDDJYNY !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ы VerbDer-V ; !
+V+Refl: VerbConj ; ! check this!!!
+V+Refl:ы VerbConj-V ; ! check this!!!

LEXICON V_LYDDJYNY !!≈ * __@CODE@__ лыддьыны:лыддь ! ярмыны. Multiple-syllable verb ending in -ыны.
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ы VerbDer-V ; !
+V: VerbConj ; ! check this!!!
+V:ы VerbConj-V ; ! check this!!!

LEXICON VR_VOLYNY !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ы VerbDer-V ; !
+V+Refl: VerbConj_KUTNY ; ! check this!!!
+V+Refl:ы VerbConj-V ; ! check this!!!

LEXICON V_VOLYNY !!≈ * __@CODE@__ волыны:лыддь ! ярмыны. Multiple-syllable verb ending in -ыны.
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
@U.CONJ-INF.YES@+V:@U.CONJ-INF.YES@ы VerbDer-V ; !
+V: VerbConj_KUTNY ; ! check this!!!
+V:ы VerbConj-V ; ! check this!!!

! First model: ы added as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ны, 
! ярм ----- ы ----/


! Second model: ы added earlier, and not as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ыны, 
! ярм ------------/



! =================== !
!! !!Verb conjugation 
! =================== !

LEXICON VerbConj-V !!≈ * __@CODE@__  суф б -- ы 
@U.CONJ-INF.YES@+Inf:@U.CONJ-INF.YES@%>ны VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.1@+Inf+Px1:@U.CONJ-INF.YES@@U.CONJ-POSS.1@%>ным VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2@+Inf+Px2:@U.CONJ-INF.YES@@U.CONJ-POSS.2@%>ныт VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3@+Inf+Px3:@U.CONJ-INF.YES@@U.CONJ-POSS.3@%>ныс VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысӧ VerbEnd-Comparison ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+ConNeg+Pl3:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@+ConNeg:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@ VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@+Imprt+Sg2:@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@ VerbEnd ;

LEXICON VerbConj !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON VerbConj_KYJNY !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms_KYJNY ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KYJNY ;

LEXICON VerbConj_AMNY !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms_AMNY ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON VerbConj_KUTNY !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms_KUTNY ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !


LEXICON Finiteforms  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present ;
 future ;
 preterite ;
 imperative ;

LEXICON Finiteforms_KYJNY  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present_KYJNY ;
 future_KYJNY ;
:й preterite_KYJNY ;
:й imperative ;

LEXICON Finiteforms_AMNY  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present_AMNY ;
 future_AMNY ;
 preterite_AMNY ;
 imperative ;

LEXICON Finiteforms_KUTNY  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present_KUTNY ;
 future_KUTNY ;
 preterite_KUTNY ;
 imperative ;

LEXICON present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>а PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON present_KYJNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>я PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@й%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON present_AMNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>а PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON present_KUTNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>а PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>а PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>ас FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON future_KYJNY
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>я PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>яс FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON future_AMNY
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>а PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>ас FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON future_KUTNY
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>а PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>ас FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON preterite
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%} PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%}  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON preterite_KYJNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON preterite_AMNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON preterite_KUTNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%} PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%}  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON imperative
@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@+Imprt+Pl2:@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ӧ VerbEnd ; !!≈ * __@CODE@__ kpv -ӧй

LEXICON PossessiveSuffixes
+PxSg1:%>ым K ;
            PossessiveSuffixes_NoSg1 ;


LEXICON PossessiveSuffixes_NoSg1
+NoPoss:     K ;
+PxSg2:%>ыд  K ;
+PxSg3:%>ыс  K ;
+PxPl1:%>ным K ;
+PxPl2:%>ныт K ;
+PxPl3:%>ныс K ;


LEXICON PresPret1  ! First and second person
@U.CONJ-NX.SG@@U.CONJ-PX.1@+Sg1:@U.CONJ-NX.SG@@U.CONJ-PX.1@ VerbEnd-Comparison ; 
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>н VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>м VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1+Err/Orth:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мӧ VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мӧй VerbEnd-Comparison ;  ! imperatiivi / optatiivi ??
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>т VerbEnd-Comparison ; 

LEXICON P3         
@U.CONJ-PX.3@:@U.CONJ-PX.3@%>с FutPret1-SgPl3 ; ! This is regular
!@U.CONJ-PX.3@  PresPret1-SgPl3 ; ! This is usually restricted to intransitives


LEXICON PresPret1-SgPl3
  SG3 ; 
  PL3 ;
  
LEXICON SG3
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
  
LEXICON PL3
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd-Comparison ;
!+Pl300@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ны@U.CONJ-NX.PL@@U.CONJ-PX.3@ VerbEnd-Comparison ;

LEXICON FutPret1-SgPl3
Fut/Pret1-Sg3 ;
Fut/Pret1-Pl3 ;
LEXICON Fut/Pret1-Sg3
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
LEXICON Fut/Pret1-Pl3
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ӧ VerbEnd-Comparison ;


LEXICON Pret2a
:а PRET2-linkki-a ;
   PRET2PL2 ; 
   PL3 ;   ! for continuation in different persons. 
                  !Could also have used the homonymic participle
                  ! as point of departure
!! Differs from kpv 2016-09-15
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>ӧсь VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ӧсь VerbEnd-Comparison ;
 

LEXICON PRET2PL2
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>ыт VerbEnd-Comparison ;
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>ат VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ныт VerbEnd-Comparison ;
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ; !2016.10.25 Цыпанов 1999: 64
!+Pl2000000@U.CONJ-NX.PL@@U.CONJ-PX.2@:%>нытӧсь@U.CONJ-NX.PL@@U.CONJ-PX.2@ VerbEnd-Comparison ; !224
!+Pl300@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ны@U.CONJ-NX.PL@@U.CONJ-PX.3@ VerbEnd-Comparison ; !227

LEXICON PRET2-linkki-a
!+Sg1+Err/Orth: VerbEnd-Comparison ; ! no Sg1 for Prt2 in the standard
@U.CONJ-NX.SG@@U.CONJ-PX.1@+Sg1:@U.CONJ-NX.SG@@U.CONJ-PX.1@ VerbEnd-Comparison ;  
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
!+Pl1+Err/Orth:%>ӧсь VerbEnd-Comparison ; ! no Pl1 for Prt2 in the standard
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>сь VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>сь VerbEnd-Comparison ;

LEXICON MatrixVerb-Comparison
               K ;
+Der/моз:%>моз K ;
+Comp:%>жык K ;
+Der/кодь:%>кодь K ;

!This is where Serial verbs are made possible
LEXICON VerbEnd-Comparison
 Serial-Verbs ;
 MatrixVerb-Comparison ;

LEXICON VerbEnd
 Serial-Verbs ;
 WordEnd ;

LEXICON Serial-Verbs
+SerialVerb:- Verbs ;


!! !!Derivation
! ==========

LEXICON Non-finiteforms
VerbDer ; ! to be merged with next
          !2011-11-02 dealing with serial verbs there may be
          ! reason to distinguish between V2V derivation and non-finite forms

LEXICON Verb-nyDer !!≈ * __@CODE@__ 
Verb-nyDer-ONSET-CONS ;
Verb-nyDer-ONSET-VOW ;

LEXICON Verb-nyDer-ONSET-CONS !!≈ * __@CODE@__ Derivation onset in consonant
!! verb-to-verb

!! This is fed by LEXICON V_SHUNY, and therefore certain corrections
!! must be made 2012-01-18

@C.CONJ-INF@@C.CONJ-POSS@+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@%>л  V_VOLYNY ; !!босьтлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@%>лывл V_VOLYNY ; !!босьтлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы:@C.CONJ-INF@@C.CONJ-POSS@%>сь V_LYDDJYNY ; !! овсьыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@%>сьышт V_SHUNY ; !!вежсьыштны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@%>сьыштлывл V_VOLYNY ; !! пусьыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@%>сьыштл V_VOLYNY ; !!?? пусьыштлыны
! зь:зь  !!югзьыны
! Why do the suffixes above have ">" marking
! while the ones below do not?
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы:@C.CONJ-INF@@C.CONJ-POSS@ч V_LYDDJYNY ; !!босьтчыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/л:@C.CONJ-INF@@C.CONJ-POSS@чыл V_SHUNY ; !!дӧзмӧдчывны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@чыштл V_VOLYNY ; !!босьтчыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@чыштлывл V_VOLYNY ; !! босьтчыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@чышт V_SHUNY ; !!босьтчыштны

LEXICON Verb-nyDer-ONSET-VOW !!≈ * __@CODE@__ Derivation onset	in vowel
 Verb-nyDer-ONSET-Y ;
 Verb-nyDer-ONSET-A ;

LEXICON Verb-nyDer-ONSET-Y !!≈ * __@CODE@__ Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ыштл V_VOLYNY ;   !!сёйыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ыштлывл V_VOLYNY ;  !!сёйыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@ышт V_SHUNY ; !!сёйыштны

LEXICON Verb-nyDer-ONSET-A !!≈ * __@CODE@__ Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/ал:@C.CONJ-INF@@C.CONJ-POSS@ал V_SHUNY ; !!сетавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/овт:@C.CONJ-INF@@C.CONJ-POSS@овт V_SHUNY ; !!пановтны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ась:@C.CONJ-INF@@C.CONJ-POSS@ась V_SHUNY ; !! босьтасьны

LEXICON Verb-nyDer-ONSET-JA !!≈ * __@CODE@__ Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/ал:@C.CONJ-INF@@C.CONJ-POSS@ял V_SHUNY ; !!сетавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/овт:@C.CONJ-INF@@C.CONJ-POSS@ёвт V_SHUNY ; !!пановтны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ась:@C.CONJ-INF@@C.CONJ-POSS@ясь V_SHUNY ; !! босьтасьны


!! verb-to-noun
 
LEXICON VerbDer !!≈ * __@CODE@__ 
 VerbDer-ONSET_OE ; !! FIX THIS 2015-09-06

LEXICON VerbDer_KYJNY !!≈ * __@CODE@__
:й VerbDer-ONSET_OE ; !!≈ * __@CODE@__
 VerbDer-ONSET_JA ; !!≈ * __@CODE@__ 
:й VerbDer-ONSET_JI ; !!≈ * __@CODE@__

LEXICON VerbDer_KUTNY !!≈ * __@CODE@__
 VerbDer-ONSET_OE ; !!≈ * __@CODE@__
 VerbDer-ONSET_A ; !!≈ * __@CODE@__ 
 VerbDer-ONSET_I ; !!≈ * __@CODE@__

LEXICON VerbDer_ARTASJNY !!≈ * __@CODE@__
:ь VerbDer-ONSET_OE ; !!≈ * __@CODE@__
 VerbDer-ONSET_JA ; !!≈ * __@CODE@__ 
 VerbDer-ONSET_JI ; !!≈ * __@CODE@__

LEXICON VerbDer-ONSET_OE !!≈ * __@CODE@__ 
+Der/ӧм+Event:%>ӧм Noun1 ;
+Der/ӧм+Event+N+Ins:%>ӧмӧн K ;
+Der/ісь+ActPrsPtc:%>%{иі%}сь N_ ; !!≈ * __@CODE@__ kpv ысь

LEXICON VerbDer-ONSET_I !!≈ * __@CODE@__ 
  ! nonfinites
+Der/иг+Duration+Ger:%>%{иі%}г K ; ! gerund (at the same time as the action)
!352 Pl
+Der/иг+Duration+Ger+Pl:%>%{иі%}гъяс K ;
+Der/иг+Duration+Ger+Pl+Ill:%>%{иі%}гъясӧ K ; !345
+Der/иг+Duration+Ger+Pl+Ill+PxSg1:%>%{иі%}гъясам K ;
+Der/иг+Duration+Ger+Pl+Ill+PxSg2:%>%{иі%}гъясад K ;
+Der/иг+Duration+Ger+Pl+Ill+PxSg3:%>%{иі%}гъясас K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl1:%>%{иі%}гъясаным K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl2:%>%{иі%}гъясаныт K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl3:%>%{иі%}гъясаныс K ;
+Der/иг+Duration+Ger+Pl+Ins+NoPoss:%>%{иі%}гъясӧн K ;! 2000:359 
                                           !And Marina 2011-10-05 no Px
+Der/иг+Duration+Ger+Sg+Ill:%>%{иі%}гӧ K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg1:%>%{иі%}гам K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg2:%>%{иі%}гад K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg3:%>%{иі%}гас K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl1:%>%{иі%}ганым K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl2:%>%{иі%}ганыт K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl3:%>%{иі%}ганыс K ;
+Der/иг+Duration+Ger+Sg+Ins:%>%{иі%}гӧн PossessiveSuffixes_NoSg1 ; !Ӧнія коми кыв, 2000: 360
!351 Pl
+Der/иг+Duration+Ger+Der/моз:%>%{иі%}гмоз PossessiveSuffixes_NoSg1 ; !371
+Der/иг+Duration+Ger+Der/сор:%>%{иі%}гсор K ;
+Der/иг+Duration+Ger+Der/сор+Ins:%>%{иі%}гсорӧн K ;
+Der/иг+Duration+Ger+Der/тыр:%>%{иі%}гтыр K ;
+Der/иг+Duration+Ger+Der/тырйи:%>%{иі%}гтырйи PossessiveSuffixes_NoSg1 ; !374
+Der/иг+Duration+Ger+Der/тыръя:%>%{иі%}гтыръя K ;
+Der/иг+Duration+Ger+Der/чӧж:%>%{иі%}гчӧж PossessiveSuffixes_NoSg1 ; !377
+Der/иг+Duration+Ger+Der/чӧж+Ins:%>%{иі%}гчӧжӧн K ; !
+Der/иг+Duration+Ger+Der/кості:%>%{иі%}гкості PossessiveSuffixes_NoSg1 ; !352, 379
+Der/иг+Duration+Ger+Der/коста:%>%{иі%}гкоста K ;
+Der/иг+Duration+Ger+Der/кежлӧ:%>%{иі%}гкежлӧ K ; !393

+Der/иг+Duration+Ger+Der/кежлӧ+PxSg2:%>%{иі%}гкежлад K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxSg3:%>%{иі%}гкежлас K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl1:%>%{иі%}гкежланым K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl2:%>%{иі%}гкежланыт K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl3:%>%{иі%}гкежланыс K ;

LEXICON VerbDer-ONSET_JI !!≈ * __@CODE@__ 
+Der/иг+Duration+Ger:%>иг K ; ! gerund (at the same time as the action)
!352 Pl
+Der/иг+Duration+Ger+Pl:%>игъяс K ;
+Der/иг+Duration+Ger+Pl+Ill:%>игъясӧ K ; !345
+Der/иг+Duration+Ger+Pl+Ill+PxSg1:%>игъясам K ;
+Der/иг+Duration+Ger+Pl+Ill+PxSg2:%>игъясад K ;
+Der/иг+Duration+Ger+Pl+Ill+PxSg3:%>игъясас K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl1:%>игъясаным K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl2:%>игъясаныт K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl3:%>игъясаныс K ;
+Der/иг+Duration+Ger+Pl+Ins+NoPoss:%>игъясӧн K ;! 2000:359 
                                           !And Marina 2011-10-05 no Px
+Der/иг+Duration+Ger+Sg+Ill:%>игӧ K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg1:%>игам K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg2:%>игад K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg3:%>игас K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl1:%>иганым K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl2:%>иганыт K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl3:%>иганыс K ;
+Der/иг+Duration+Ger+Sg+Ins:%>игӧн PossessiveSuffixes_NoSg1 ; !Ӧнія коми кыв, 2000: 360
!351 Pl
+Der/иг+Duration+Ger+Der/моз:%>игмоз PossessiveSuffixes_NoSg1 ; !371
+Der/иг+Duration+Ger+Der/сор:%>игсор K ;
+Der/иг+Duration+Ger+Der/сор+Ins:%>игсорӧн K ;
+Der/иг+Duration+Ger+Der/тыр:%>игтыр K ;
+Der/иг+Duration+Ger+Der/тырйи:%>игтырйи PossessiveSuffixes_NoSg1 ; !374
+Der/иг+Duration+Ger+Der/тыръя:%>игтыръя K ;
+Der/иг+Duration+Ger+Der/чӧж:%>игчӧж PossessiveSuffixes_NoSg1 ; !377
+Der/иг+Duration+Ger+Der/чӧж+Ins:%>игчӧжӧн K ; !
+Der/иг+Duration+Ger+Der/кості:%>игкості PossessiveSuffixes_NoSg1 ; !352, 379
+Der/иг+Duration+Ger+Der/коста:%>игкоста K ;
+Der/иг+Duration+Ger+Der/кежлӧ:%>игкежлӧ K ; !393

+Der/иг+Duration+Ger+Der/кежлӧ+PxSg2:%>игкежлад K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxSg3:%>игкежлас K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl1:%>игкежланым K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl2:%>игкежланыт K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl3:%>игкежланыс K ;


LEXICON VerbDer-ONSET_A !!≈ * __@CODE@__ 
+ANA+Adv:%>ана K ;
+ANAA+Adv:%>анаа K ; !Adding after attestation in Komi Grammar 2000: 381
+AN+Der/моз:%>анмоз PossessiveSuffixes_NoSg1 ; !383

LEXICON VerbDer-ONSET_JA !!≈ * __@CODE@__ 
+ANA+Adv:%>яна K ;
+ANAA+Adv:%>янаа K ; !Adding after attestation in Komi Grammar 2000: 381
+AN+Der/моз:%>янмоз PossessiveSuffixes_NoSg1 ; !383




LEXICON Verb-ynyDer
! verb-yny-to-verb
! The source stems have no stem vowel "ы", and therefore
! dervational suffixes come with an onset vowel

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim:@C.CONJ-INF@@C.CONJ-POSS@ышт V_SHUNY ; !!кольмыштны
!+V+Der/ышт+Dim+V:%>ышт  VerbConj ;
!+V+Der/ышт+Dim+V+Inf:%>ыштны K ;
!+V+Der/ышт+Dim+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ыштны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+V+Der/ышт+Dim+V+ConNeg@U.CONJ-CONNEG.YES@:%>ышт0@U.CONJ-CONNEG.YES@ WordEnd ;
!+V+Der/ышт+Dim+V+Imprt+Sg2:%>ышт WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ыштл V_VOLYNY ;   !!??кольмыштлыны
!+V+Der/ышт+Dim+V+Der/лы+V:%>ыштл  VerbConj ;
!+V+Der/ышт+Dim+V+Der/лы+V+Inf:%>ыштлыны K ;
!+V+Der/ышт+Dim+V+Der/лы+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ыштлыны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+V+Der/ышт+Dim+V+Der/лы+V+ConNeg@U.CONJ-CONNEG.YES@:%>ыштл00@U.CONJ-CONNEG.YES@ WordEnd ;
!+V+Der/ышт+Dim+V+Der/лы+V+Imprt+Sg2:%>ыштл WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ыштлывл V_VOLYNY ;  !!??кольмыштлывлыны

@C.CONJ-INF@@C.CONJ-POSS@+Der/л:@C.CONJ-INF@@C.CONJ-POSS@ыл V_SHUNY ;  !!тшӧктывны

@C.CONJ-INF@@C.CONJ-POSS@+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ывл V_VOLYNY ; !!тшӧктывлыны
!+V+Der/ывлы+V:%>ывл VerbConj ; ! мынтывл - мынтывлан
!+V+Der/ывлы+V+Inf:%>ывлыны K ;
!+V+Der/ывлы+V+ConNeg000@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ывлыны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+V+Der/ывлы+V+ConNeg0@U.CONJ-CONNEG.YES@:%>ывлы@U.CONJ-CONNEG.YES@ WordEnd ;
!+V+Der/ывлы+V+Imprt+Sg2:%>ывлы WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы:@C.CONJ-INF@@C.CONJ-POSS@ысь V_LYDDJYNY ; !! вевттьысьыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сь+Der/ышт+Dim:@C.CONJ-INF@@C.CONJ-POSS@ысьышт V_SHUNY ; !!лэптысьыштны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ысьыштлывл V_VOLYNY ; !!?? вевттьысьыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ысьыштл V_VOLYNY ; !!лэптысьыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сь+Der/л:@C.CONJ-INF@@C.CONJ-POSS@ысьл V_VOLYNY ; !!кутчысьлыны

@C.CONJ-INF@@C.CONJ-POSS@+Der/ал:@C.CONJ-INF@@C.CONJ-POSS@ал V_SHUNY ; !!бергӧдчавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ал+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@авл V_VOLYNY ; !!бергӧдчавлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ась:@C.CONJ-INF@@C.CONJ-POSS@ась V_SHUNY ; !! бертласьны


!<!-- This far 2012-01-19 -->

!+Der/овт@C.CONJ-INF@@C.CONJ-POSS@:овт@C.CONJ-INF@@C.CONJ-POSS@ V_SHUNY ; !!пановтны


! Stray verb forms :-о


LEXICON VerbDer-V
! Page 346 in the Komi grammar
+Der/мысьт:%>мысьт K ; ! gerund (after the action) !353
+Der/мысь:%>мысь K ; ! !353
+Der/тӧдз:%>тӧдз PossessiveSuffixes_NoSg1 ; !396
+Der/мӧн:%>мӧн PossessiveSuffixes_NoSg1 ; !399-403
+Der/мӧнъя:%>мӧнъя WordEnd ; !399-403
+Der/тӧм+VAbe:%>тӧм PossessiveSuffixes ;
+Car:%>тӧг PossessiveSuffixes_NoSg1 ; ! 390
+Car+Err/Orth:%>тӧгъя K ; !387
+Car+Err/Orth:%>тӧга K ; !387
+Car+Der/моз:%>тӧгмоз PossessiveSuffixes_NoSg1 ; !



! 2012-02-11 This is a problem; олан-вӧлан, овны-вӧвны
LEXICON VerbPRT ! one verb, вӧвны 
+V: preterite ;
@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+V+ConNeg+Pl3:@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны WordEnd ;
@U.CONJ-CONNEG.YES@+V+ConNeg:@U.CONJ-CONNEG.YES@ WordEnd ;

LEXICON EMYSHT ! эмышт "is a little / are a few"
@U.CONJ-MX.IND@+V+Ind+Prs+Sg3:@U.CONJ-MX.IND@ WordEnd ;
@U.CONJ-MX.IND@@U.CONJ-NX.PL@@U.CONJ-PX.3@+V+Ind+Prs+Pl3:@U.CONJ-MX.IND@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
 
LEXICON GER ! 3 lexicalised +Ger forms.
+V+Ger: WordEnd ;

! vim: set ft=xfst-lexc:
