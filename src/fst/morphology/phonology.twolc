!! =================================== !
!! The Komi-Permyak morphophonological/twolc rules file 
!! =================================== !

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-koi/blob/main/src/fst/phonology.twolc) 

!! ## Alphabet, Sets and Definitions

Alphabet

!! ## Letters of the alphabet

а б в г д е ё ж з и і й к л м н о ӧ п р с т у ф х ц ч ш щ ъ ы ь э ю я !!= * @CODE@
А Б В Г Д Е Ё Ж З И І Й К Л М Н О Ӧ П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я !!= * @CODE@
к2:к л2:л м2:м т2:т ь2:ь
!К2 Л2 М2 Т2 Ь2 
И2:и
 %{иі%}:и
 %{иі%}:і
 %{ая%}:а
 %{ая%}:я

!! ## Archiphonemes for vowels
!! ## Triggers
%^1Cns:0   !!= * @CODE@ Plural-initial; it allows for doubling of consonant

%^LVBound:0    !!= * @CODE@ boundary for LV change instead of Consonant or end of word
%^VowRM:0      !!= * @CODE@ vowel syncope
%^SoftRet:0    !!= * @CODE@ soft sign retention Дӧсвидзисьсянь (Bible); literary norm Дӧсвидзиссянь (possibly ambiguous somewhere)

!! ## Boundary symbols

 %> !!= * @CODE@ morpheme boundary
 #  !!  * hash # word boundary
 %- !!= * @CODE@ 
 »7     !!= * »
 «7     !!= * «
 %[%>%] !!= * > (escaped with square brackets, to avoid collision with > as morpheme boundary)
 %[%<%] !!= * < (escaped with square brackets, to avoid collision with < as morpheme boundary)

!! Diacritics
%^Inf:0  %^PATR:0 %^FEM:0 


 ;


!! ## Sets 

Sets
!! Vowel
Vow =  а е ё и і о ӧ у ы э ю я 
       А Е Ё И У О Ӧ У Ы Э Ю Я ;

!! Palatal Vowel
!! Cns-initial vowels
PalVow = е ё ю я
         Е Ё Ю Я ;

!! All non-vowels, consonants and hard and soft signs
Cns =  б в г д ж з й к л м н п р с т ф х ц ч ш щ ъ ь
       Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь
       к2 л2 м2 т2 ь2 ;
!  К2 Л2 М2 Т2 Ь2 

!! All non-vowels with exception of soft sign
CnsNotSoft =  б в г д ж з й к л м н п р с т ф х ц ч ш щ ъ
              Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Ъ
       	      к2 л2 м2 т2 ь2 ;
!   К2 Л2 М2 Т2 Ь2 

!! All consonants with hard A
CnsWithHardA =  б в г д ж з к л м н п р с т ф х ц ч ш щ
                Б В Г Д Ж З К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;

!! All consonants followed by Cyrillic и
CnsBeforeCyri = б в г ж й к м п р ф х ч ш щ
              	Б В Г Ж Й К М П Р Ф Х Ч Ш Щ
              	к2 м2 т2 ;
! К2 М2 Т2 
!! Why is т2 here 2015-10-01
PairedCnsNoZ =  д л н с т ц
                Д Л Н С Т Ц
                л2 т2 ;


!! Letters
 Letter = Vow Cns ;

!! Dummy
Dummy = %> %^Inf %^PATR %^FEM ;


!! ## Definitions

!Definitions


!! ## Rules

Rules

"Double stem-final consonant"
!! **@RULENAME@**
 %^1Cns:Cy <=> Cy (ь:) %> _ э: ;
          where Cy in (б г д ж з к л м н п р с т ф х ц ч ш щ) ;


"Double stem-final consonant л:в"
!! **@RULENAME@**
 %^1Cns:в <=> [ в | л: %^LVBound: ] %> _ э: ;


"%^1Cns to zero"
!! **@RULENAME@**
 %^1Cns:0 <=> [Vow|й ] %> _ э: ;

"э:е"
!! **@RULENAME@**
э:е <=> [CnsBeforeCyri | л: %^LVBound: |ь:|й|д з|ц] %> %^1Cns: _ [з|с|т] ;

"ы:0"
ы:0 <=> Cns (ь:) _ %^VowRM: ;
        р й: _ (%^VowRM:) %> %{ая%}: ;

"The famous L/V"
!! **@RULENAME@**
 л:в <=> Vow _ %^LVBound:0 ;
! Vow _ [ # | %> Cns ] ;

!! Paragogic consonants
"mDeletion"
!! **@RULENAME@**
 м:0 <=> н _ [ # | %> Cns ] ;
 !!€ синм%>ыс
 !!€ синм%>ыс
 !!€ синм#
 !!€ син0#
 !!€ синм%>сӧ
 !!€ син0%>сӧ

!! Paragogic consonants
"tDeletion"
!! **@RULENAME@**
 т:0 <=> п _ [ # | %> Cns ] ;
 !!€ зепт%>ыс
 !!€ зепт%>ыс
 !!€ зепт#
 !!€ зеп0#
 !!€ зепт%>сӧ
 !!€ зеп0%>сӧ

!"kDeletion"
!!! __@RULENAME@__
! к:0 <=> [ с | ш | с ь | с ь2: | ч ] _ [ # | %> Cns ] ;
! !!€ воск%>ыс
! !!€ воск%>ыс
! !!€ воск#
! !!€ вос0#
! !!€ воск%>сӧ
! !!€ вос0%>сӧ
! !!€ кось2к%>сянь
! !!€ кось0%>сянь

"jDeletion after vowel"
!! **@RULENAME@**
 й:0 <=> [б | в | г | д | ж | з | к | м | н | п | р | т | с ] _ [ # | %> Cns ] ;
 !!€ гобй%>ыс
 !!€ гобй%>ыс
 !!€ гобй#
 !!€ гоб0#
 !!€ гобй%>сӧ
 !!€ гоб0%>сӧ
 !!€ кывй%>ыс
 !!€ кывй%>ыс
 !!€ кывй#
 !!€ кыв0#
 !!€ кывй%>сӧ
 !!€ кыв0%>сӧ
   Vow _ %> [ а: | %{иі%}: | %{ая%}: ] ;
 !!€ вый%>а#
 !% вы0%>я#
 !!€ сёй%>а#
 !!€ сё0%>я#
 !!€ сёй%>%{иі%}#
 !!€ сё0%>и#

"j to soft sign after r"
!! **@RULENAME@**
 й:ь <=> [ б | в | г | ж | к | м | п | р | ч | ш ] _ %> а: ;
  [ б | в | г | ж | к | м | п | р | ч | ш ] _ ы: (%^VowRM:) %> %{ая%}: ;

!!€ дорйы^VowRM>{ая}
!!€ дорь00>я
!!€ кывй%>а#
!!€ кывь%>я#

"j to hard sign after consonant"
!! **@RULENAME@**
 й:ъ <=> [ д | з | н | т | с ] _ %> а: ;
! й:ъ <=> [б | в | г | д | ж | з | к | м | н | п | р | т | с ] _ %> а: ;
!!€ кӧсй%>а#
!!€ кӧсъ%>я#

"l deletion"
!! **@RULENAME@**
 л:0 <=>  л _  ь  [ # | %> Cns ] ;
 !!€ вакулль%>ыс
 !!€ вакулль%>ыс
 !!€ вакулль#
 !!€ вакул0ь#
 !!€ вакулль%>сӧ
 !!€ вакул0ь%>сӧ

"d deletion"
!! **@RULENAME@**
 д:0 <=>  _ д [ ь | з ]  [ # | %> Cns ] ;
 !!€ виддз%>ыс
 !!€ виддз%>ыс
 !!€ виддз#
 !!€ вид0з#
 !!€ виддз%>сӧ
 !!€ вид0з%>сӧ
          _  д ь: %> %^1Cns: ;
 !!€ баддь%>%^1Cnsэз
 !!€ ба0д0%>дез

!! Vowel Palatalisation
"а 2 я, о 2 ё, у 2 ю"
!! **@RULENAME@**
 Vx:Vy <=> [~[ д з | ч | в | б | г | к | м | п | р ]] [ ь: | й: ]  %>  _ [ # | %> | Cns: | Vow: ] ;
        where Vx in (а о у)
              Vy in (я ё ю)
        matched ;
!!€ вой%>ас
!!€ во0%>яс
!!€ озй%>ас
!!€ озъ%>яс
!!€ ворссь%>а#
!!€ ворсс0%>я#
!! картопель+Sg+Ine+PxSg3
!!€ картопель%>ас
!!€ картопел0%>яс
!! картопель+Pl+Nom
!!€ картопель%>ъяс
!!€ картопел0%>0яс
! Have to look for ь>о:ё, 
! ь>у:ю appears in compound words

"%{иі%} 2 і"
!! **@RULENAME@**
 %{иі%}:і <=>
 PairedCnsNoZ (ы: %^VowRM:) %>  _ [ # | %> | Cns: | Vow: ] ;
 [Vow: |Vow: [Cns - д]] (з) з  (ы: %^VowRM:) %>  _ [ # | %> | Cns: | Vow: ] ;



"%{иі%} 2 и"
!! **@RULENAME@**
 %{иі%}:и <=> [ Vow: | CnsBeforeCyri | й: | ь: | Vow:  д (з) з ] (ы: %^VowRM:) %>  _ [ # | %> | Cns: | Vow: ] ;
!!€ ворссь%>%{иі%}#
!!€ ворсс0%>и#
!!€ ло%>%{иі%}#
!!€ ло%>и#
!!€ шу%>%{иі%}с
!!€ шу%>ис
!!€ вай%>%{иі%}с
!!€ вай%>ис

"%{ая%} 2 а"
!! **@RULENAME@**
 %{ая%}:а <=>
 [CnsWithHardA | Vow ] (ы: %^VowRM:) %>  _ [ # | %- | %> | Cns: | Vow: ] ;
!!€ ло%>%{ая%}#
!!€ ло%>а#
!!€ шу%>%{ая%}с
!!€ шу%>ас



"%{ая%} 2 я"
!! **@RULENAME@**
 %{ая%}:я <=> [  й: | ь: ] (ы: %^VowRM:) %>  _ [ # | %- | %> | Cns: | Vow: ] ;
!!€ ворссь%>%{ая%}#
!!€ ворсс0%>я#
!!€ вай%>%{ая%}с
!!€ вай%>яс


"Soft Sign Deletion"
!! **@RULENAME@**
 ь:0 <=>  Cns _ %> %^1Cns: ;
!!€ нянь%>%^1Cnsэз
!!€ нян0%>нез
 Cns _ (ы: %^VowRM:) %> [ а: | о: | у: | і: | %{иі%}: | %{ая%}: ] ; 
        с _  %> с [ ь | я ] ;
 
! Testcases: see VowelPalatalisation above
! Also orthography _Коми орфография кывкуд 2008: 9_
! ь2, Ь2 will be used to mark special instances of _ь_ retension
!!€ велӧдысь%>сянь
!!€ велӧдыс0%>сянь
!!€ велӧдысь%>сьыс
!!€ велӧдыс0%>сьыс

"Hard Sign Deletion"
!! **@RULENAME@**
 ъ:0 <=> [ Vow (й) | ь ] %> _ ;
!!€ вой%>ъяс
!!€ вой%>0яс
!!€ выль%>ъяс
!!€ выль%>0яс
!!€ коми%>ъяс
!!€ коми%>0яс
!!€ автобус%>ъяс
!!€ автобус%>ъяс
!!$ автобус%>0яс

"Hard Sign Palatalization"
!! **@RULENAME@**
 ъ:ь <=> [ д з | ч ] %> _ я ;
!!€ виддз%>ъяс
!!€ вид0з%>ьяс

"No triple letters"
!! **@RULENAME@**
 Cx:0 <=> Cx _ %> Cx ;
        where Cx in (а о у) ;
! Also orthography _Коми орфография кывкуд 2008: 16_
!!€ класс%>сянь
!!€ клас0%>сянь
!!€ Блатт%>тӧг
!!€ Блат0%>тӧг
!!€ металл%>лӧн
!!€ метал0%>лӧн

"IClitic"
!! **@RULENAME@**
 И2:й <=> Vow %- _ #  ;


"Disallow soft retention when not сь or зь"
!! **@RULENAME@**  
%^SoftRet:0 /<= [[ :* Letters Letters ] - [ :* [ с | з ] ь: ] ] _ %> с я ;

