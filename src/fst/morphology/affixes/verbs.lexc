

! ====================================== !
!! # Verbal morphology               
! ====================================== !

LEXICON Verbs
verb    V_   ;
!verbs ; 
  V_koi2x ;

!LEXICON V_NO-FST
!+V: K ;

LEXICON V_
+WORK: # ;


!! ## Closed class verbs
! ------------------
LEXICON AUXILIARIES
бы+Pcle+Aux+Cond:бы WordEnd ;
вай+Pcle+Aux+Imprt:вай WordEnd ;
вайко+Pcle+Aux+Imprt:вайко WordEnd ;

LEXICON VERBNEGATIVE
!Present Neg
оз+V+Neg+Ind+Prs+Sg1:ог MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg2:он MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg3:оз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:ог MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:од MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:одӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl3:оз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl3:озӧ MatrixVerb-Comparison ;


!Past Neg
оз+V+Neg+Ind+Prt1+Sg1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Sg2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Sg3:эз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl2:эд MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl1:эгӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl2:эдӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl3:эз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl3:эзӧ MatrixVerb-Comparison ;

!Imperative
оз+V+Neg+Imprt+Sg2:эн K ;
оз+V+Neg+Imprt+Pl2:эн K ;
оз+V+Neg+Imprt+Pl2:энӧ K ;
!A tag is needed to indicate this clitic-like лы segment
оз+V+Neg+Imprt+Sg2:энлы K ;

! Negative copula supletive present forms
абу+V+Neg+Cop+Ind+Sg:абу K ; ! Aux these are supletive. Past is эз вӧв
абу+V+Neg+Cop+Ind+Pl:абуӧсь K ; ! Aux these are supletive. Past is эз вӧвны


!! ## Open class verbs
! ----------------

!! Some Flag diacritic lines are with regexes, other with aligned zeros. We want to migrate to
!! regexes < … > , for readability reasons (sic!)

LEXICON V_LOKNY !!≈ * **@CODE@** One verb only, локны:лок U9 — глагол локны
@U.CONJ-INF.YES@+Inf:@U.CONJ-INF.YES@%>ны VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.1@+Inf+Px1:@U.CONJ-INF.YES@@U.CONJ-POSS.1@%>ным VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2@+Inf+Px2:@U.CONJ-INF.YES@@U.CONJ-POSS.2@%>ныт VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3@+Inf+Px3:@U.CONJ-INF.YES@@U.CONJ-POSS.3@%>ныс VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысӧ VerbEnd-Comparison ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+ConNeg+Pl3:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@+ConNeg:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@ VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@+Imprt+Sg2:@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@ VerbEnd ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%>т VerbDer_KUTNY ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%>т VerbDer-V ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%>ты VerbDer-V ; !CHECK Which one to prefer 2023-12-20
:т  VerbConj_KUTNY ; !а


!! Begin work with TV and IV
LEXICON BV_SHUNY
TV_SHUNY ;
IV_SHUNY ;
LEXICON BV_JUAYSHNY
TV_JUAYSHNY ;
IV_JUAYSHNY ;
LEXICON BV_AMNY
TV_AMNY ;
IV_AMNY ;
LEXICON BV_KYJNY !U6 — глагол йны
TV_KYJNY ;
IV_KYJNY ;
LEXICON BV_CHILOESNY
TV_CHILOESNY ;
IV_CHILOESNY ;
LEXICON BV_KUTNY
TV_KUTNY ;
IV_KUTNY ;
LEXICON BV_NUAVNY !U7 — глагол вны
TV_NUAVNY ;
IV_NUAVNY ;
LEXICON BV_ARTASJNY
TV_ARTASJNY ;
IV_ARTASJNY ;
LEXICON BV_LYDDJYNY
TV_LYDDJYNY ;
IV_LYDDJYNY ;
LEXICON BV_VOLYNY
TV_VOLYNY ;
IV_VOLYNY ;

LEXICON TV_SHUNY
+TV: V_SHUNY ;
LEXICON TV_JUAYSHNY
+TV: V_JUAYSHNY;
LEXICON TV_AMNY
+TV: V_AMNY ;
LEXICON TV_KYJNY
+TV: V_KYJNY;
LEXICON TV_CHILOESNY
+TV: V_CHILOESNY ;
LEXICON TV_KUTNY
+TV: V_KUTNY ;
LEXICON TV_NUAVNY
+TV: V_NUAVNY ;
LEXICON TV_ARTASJNY
+TV: V_ARTASJNY ;
LEXICON TV_LYDDJYNY
+TV: V_LYDDJYNY ;
LEXICON TV_VOLYNY
+TV: V_VOLYNY ;

LEXICON IV_SHUNY
+IV: V_SHUNY ;
LEXICON IV_JUAYSHNY
+IV: V_JUAYSHNY;
LEXICON IV_AMNY
+IV: V_AMNY ;
LEXICON IV_KYJNY
+IV: V_KYJNY;
LEXICON IV_CHILOESNY
+IV: V_CHILOESNY ;
LEXICON IV_KUTNY
+IV: V_KUTNY ;
LEXICON IV_NUAVNY
+IV: V_NUAVNY ;
LEXICON IV_ARTASJNY
+IV: V_ARTASJNY ;
LEXICON IV_LYDDJYNY
+IV: V_LYDDJYNY ;
LEXICON IV_VOLYNY
+IV: V_VOLYNY ;

LEXICON VR_SHUNY !!≈ * **@CODE@** 
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+Refl: VerbConj ; !а
+Refl: VerbConj-V ; !б

LEXICON V_SHUNY !!≈ * **@CODE@** шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj ; !а
: VerbConj-V ; !б

LEXICON V_JUAYSHNY !!≈ * **@CODE@** is there ш : шт gradation
 V_AMNY ; 
!LEXICON VR_AMNY !!≈ * __@CODE@__
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
!+Refl: VerbConj_AMNY ; !а
!+Refl: VerbConj-V ; !б

LEXICON V_AMNY !!≈ * **@CODE@** амны:ам Ending in -мны, -жны, -дзны,
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj_AMNY ; !а
: VerbConj-V ; !б

!LEXICON VR_KYJNY !!≈ * __@CODE@__ 
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-CONS ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-Y ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-JA ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer_KYJNY ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
!+Refl: VerbConj_KYJNY ; !а
!+Refl:й VerbConj-V ; !б

LEXICON V_KYJNY !!≈ * **@CODE@** кыйны:кый Ending in -йны, 
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-Y ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-JA ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KYJNY ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj_KYJNY ; !а
: VerbConj-V ; !б

LEXICON V_CHILOESNY !!≈ * **@CODE@** is there s : st alternation 2018-09-01
 V_KUTNY ;

!LEXICON VR_KUTNY !!≈ * __@CODE@__
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
!+Refl: VerbConj_KUTNY ; !а
!+Refl: VerbConj-V ; !б

LEXICON V_KUTNY !!≈ * **@CODE@** шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj_KUTNY ; !а
: VerbConj-V ; !б

!LEXICON VR_NUAVNY !!≈ * __@CODE@__ нуавны:нуа
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@в Verb-nyDer-ONSET-CONS ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@л Verb-nyDer-ONSET-VOW ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@л VerbDer_KUTNY ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@в VerbDer-V ; !
!+Refl:л VerbConj_KUTNY ; !а
!+Refl:в VerbConj-V ; !б

LEXICON V_NUAVNY !!≈ * **@CODE@** нуавны:нуа
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%^LVBound Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-VOW ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%^LVBound VerbDer-V ; !
: VerbConj_KUTNY ; !а
:%^LVBound VerbConj-V ; !! Consonant-onset

!LEXICON VR_ARTASJNY !!≈ * __@CODE@__
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ь Verb-nyDer-ONSET-CONS ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ь Verb-nyDer-ONSET-Y ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-JA ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer_ARTASJNY ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ь VerbDer-V ; !
!+Refl: VerbConj ; !а
!+Refl: VerbConj-V ; !б

LEXICON V_ARTASJNY !!≈ * **@CODE@** артасьны:артас -Cны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_ARTASJNY ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj ; !а
: VerbConj-V ; !б

LEXICON VR_LYDDJYNY !!≈ * **@CODE@** 
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@%^VowRM Verb-ynyDer ; !
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@%^VowRM VerbDer ; !
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ы VerbDer-V ; !
+Refl:%^VowRM VerbConj ; ! check this!!!
+Refl: VerbConj-V ; ! check this!!!

LEXICON V_LYDDJYNY !!≈ * **@CODE@** лыддьыны:лыддьы ! ярмыны. Multiple-syllable verb ending in -ыны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%^VowRM Verb-ynyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%^VowRM VerbDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
:%^VowRM VerbConj ; ! check this!!!
: VerbConj-V ; ! check this!!!

!LEXICON VR_VOLYNY !!≈ * __@CODE@__ 
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ Verb-ynyDer ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !
!@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@ы VerbDer-V ; !
!+Refl: VerbConj_KUTNY ; ! check this!!!
!+Refl:ы VerbConj-V ; ! check this!!!

LEXICON V_VOLYNY !!≈ * **@CODE@** волыны:волы ! ярмыны. Multiple-syllable verb ending in -ыны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%^VowRM Verb-ynyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%^VowRM VerbDer_KUTNY ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
:%^VowRM VerbConj_KUTNY ; ! check this!!!
: VerbConj-V ; ! check this!!!

! First model: ы added as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ны, 
! ярм ----- ы ----/


! Second model: ы added earlier, and not as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ыны, 
! ярм ------------/



! =================== !
!! ## Verb conjugation 
! =================== !

LEXICON VerbConj-V !!≈ * **@CODE@**  суф б -- ы 
@U.CONJ-INF.YES@+Inf:@U.CONJ-INF.YES@%>ны VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.1@+Inf+Px1:@U.CONJ-INF.YES@@U.CONJ-POSS.1@%>ным VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2@+Inf+Px2:@U.CONJ-INF.YES@@U.CONJ-POSS.2@%>ныт VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3@+Inf+Px3:@U.CONJ-INF.YES@@U.CONJ-POSS.3@%>ныс VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысӧ VerbEnd-Comparison ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+ConNeg+Pl3:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@+ConNeg:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@ VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@+Imprt+Sg2:@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@ VerbEnd ;

LEXICON VerbConj !!≈ * **@CODE@** суф а
@U.CONJ-INF.NO@ Finiteforms ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON VerbConj_KYJNY !!≈ * **@CODE@** суф а
@U.CONJ-INF.NO@ Finiteforms_KYJNY ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KYJNY ;

LEXICON VerbConj_AMNY !!≈ * **@CODE@** суф а
@U.CONJ-INF.NO@ Finiteforms_AMNY ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON VerbConj_KUTNY !!≈ * **@CODE@** суф а
@U.CONJ-INF.NO@ Finiteforms_KUTNY ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_KUTNY ; !


LEXICON Finiteforms  !!≈ * **@CODE@** Gives linking vowels for 3 tenses
 present ;
 future ;
 preterite ;
 imperative ;

LEXICON Finiteforms_KYJNY  !!≈ * **@CODE@** Gives linking vowels for 3 tenses
 present_KYJNY ;
 future_KYJNY ;
: preterite_KYJNY ;
: imperative ;

LEXICON Finiteforms_AMNY  !!≈ * **@CODE@** Gives linking vowels for 3 tenses
 present_AMNY ;
 future_AMNY ;
 preterite_AMNY ;
 imperative ;

LEXICON Finiteforms_KUTNY  !!≈ * **@CODE@** Gives linking vowels for 3 tenses
 present_KUTNY ;
 future_KUTNY ;
 preterite_KUTNY ;
 imperative ;

LEXICON present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>%{ая%} PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON present_KYJNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>%{ая%} PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON present_AMNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>%{ая%} PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON present_KUTNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>%{ая%} PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON future_KYJNY
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON future_AMNY
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON future_KUTNY
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с FutPret1-SgPl3 ; ! 3 FutPret1-SgPl3   ХХХ check this

LEXICON preterite
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%} PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%}  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON preterite_KYJNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON preterite_AMNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>и  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON preterite_KUTNY
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%} PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%}  P3 ; ! 3 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;

LEXICON imperative
@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@+Imprt+Pl2:@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ӧ VerbEnd ; !!≈ * **@CODE@** kpv -ӧй

LEXICON PossessiveSuffixes
+PxSg1:%>ым K ;
            PossessiveSuffixes_NoSg1 ;


LEXICON PossessiveSuffixes_NoSg1
+NoPoss:     K ;
+PxSg2:%>ыт  K ;
+PxSg3:%>ыс  K ;
+PxPl1:%>ным K ;
+PxPl2:%>ныт K ;
+PxPl3:%>ныс K ;


LEXICON PresPret1  ! First and second person
@U.CONJ-NX.SG@@U.CONJ-PX.1@+Sg1:@U.CONJ-NX.SG@@U.CONJ-PX.1@%>  VerbEnd-Comparison ; 
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>н VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>м VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1+Err/Orth:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мӧ VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мӧй VerbEnd-Comparison ;  ! imperatiivi / optatiivi ??
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>т VerbEnd-Comparison ; 

LEXICON P3         
@U.CONJ-PX.3@:@U.CONJ-PX.3@%>с FutPret1-SgPl3 ; ! This is regular
!@U.CONJ-PX.3@  PresPret1-SgPl3 ; ! This is usually restricted to intransitives


LEXICON PresPret1-SgPl3
  SG3 ; 
  PL3 ;
  
LEXICON SG3
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
  
LEXICON PL3
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd-Comparison ;
!+Pl300@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ны@U.CONJ-NX.PL@@U.CONJ-PX.3@ VerbEnd-Comparison ;

LEXICON FutPret1-SgPl3
Fut/Pret1-Sg3 ;
Fut/Pret1-Pl3 ;
LEXICON Fut/Pret1-Sg3
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
LEXICON Fut/Pret1-Pl3
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ӧ VerbEnd-Comparison ;


LEXICON Pret2a
:а PRET2-linkki-a ;
   PRET2PL2 ; 
   PL3 ;   ! for continuation in different persons. 
                  !Could also have used the homonymic participle
                  ! as point of departure
!! Differs from kpv 2016-09-15
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>ӧсь VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ӧсь VerbEnd-Comparison ;
 

LEXICON PRET2PL2
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>ыт VerbEnd-Comparison ;
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>%{ая%}т VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ныт VerbEnd-Comparison ;
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ; !2016.10.25 Цыпанов 1999: 64
!+Pl2000000@U.CONJ-NX.PL@@U.CONJ-PX.2@:%>нытӧсь@U.CONJ-NX.PL@@U.CONJ-PX.2@ VerbEnd-Comparison ; !224
!+Pl300@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ны@U.CONJ-NX.PL@@U.CONJ-PX.3@ VerbEnd-Comparison ; !227

LEXICON PRET2-linkki-a
!+Sg1+Err/Orth: VerbEnd-Comparison ; ! no Sg1 for Prt2 in the standard
@U.CONJ-NX.SG@@U.CONJ-PX.1@+Sg1:@U.CONJ-NX.SG@@U.CONJ-PX.1@ VerbEnd-Comparison ;  
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
!+Pl1+Err/Orth:%>ӧсь VerbEnd-Comparison ; ! no Pl1 for Prt2 in the standard
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>сь VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>сь VerbEnd-Comparison ;

LEXICON MatrixVerb-Comparison
               K ;
+Der+Der/AdvCompMod:%>моз K ;
+Comp:%>жык K ;
+Der+Der/CompMod:%>кодь K ;

!This is where Serial verbs are made possible
LEXICON VerbEnd-Comparison
 Serial-Verbs ;
 MatrixVerb-Comparison ;

LEXICON VerbEnd
 Serial-Verbs ;
 WordEnd ;

LEXICON Serial-Verbs
+Cmp/Serial+Cmp#:%-# Verbs ;
+Cmp/Serial+Cmp#:%-# V_NEWWORDS ;


!! ## Derivation
! ==========

LEXICON Non-finiteforms
VerbDer ; ! to be merged with next
          !2011-11-02 dealing with serial verbs there may be
          ! reason to distinguish between V2V derivation and non-finite forms

LEXICON Verb-nyDer !!≈ * **@CODE@** 
Verb-nyDer-ONSET-CONS ;
Verb-nyDer-ONSET-VOW ;

LEXICON Verb-nyDer-ONSET-CONS !!≈ * **@CODE@** Derivation onset in consonant
!! verb-to-verb

!! This is fed by LEXICON V_SHUNY, and therefore certain corrections
!! must be made 2012-01-18

@C.CONJ-INF@@C.CONJ-POSS@+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@%>л  V_VOLYNY ; !!босьтлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@%>лывл V_VOLYNY ; !!босьтлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы:@C.CONJ-INF@@C.CONJ-POSS@%>сь V_LYDDJYNY ; !! овсьыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@%>сьышт V_SHUNY ; !!вежсьыштны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@%>сьыштлывл V_VOLYNY ; !! пусьыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@%>сьыштл V_VOLYNY ; !!?? пусьыштлыны
! зь:зь  !!югзьыны
! Why do the suffixes above have ">" marking
! while the ones below do not?
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы:@C.CONJ-INF@@C.CONJ-POSS@ч V_LYDDJYNY ; !!босьтчыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/л:@C.CONJ-INF@@C.CONJ-POSS@чыл V_SHUNY ; !!дӧзмӧдчывны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@чыштл V_VOLYNY ; !!босьтчыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@чыштлывл V_VOLYNY ; !! босьтчыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@чышт V_SHUNY ; !!босьтчыштны

LEXICON Verb-nyDer-ONSET-VOW !!≈ * **@CODE@** Derivation onset	in vowel
 Verb-nyDer-ONSET-Y ;
 Verb-nyDer-ONSET-A ;

LEXICON Verb-nyDer-ONSET-Y !!≈ * **@CODE@** Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ыштл V_VOLYNY ;   !!сёйыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ыштлывл V_VOLYNY ;  !!сёйыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@ышт V_SHUNY ; !!сёйыштны

LEXICON Verb-nyDer-ONSET-A !!≈ * **@CODE@** Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/Al:@C.CONJ-INF@@C.CONJ-POSS@ал V_SHUNY ; !!сетавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/овт:@C.CONJ-INF@@C.CONJ-POSS@овт V_SHUNY ; !!пановтны
@C.CONJ-INF@@C.CONJ-POSS@+Der/Asj:@C.CONJ-INF@@C.CONJ-POSS@ась V_SHUNY ; !! босьтасьны

LEXICON Verb-nyDer-ONSET-JA !!≈ * **@CODE@** Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/Al:@C.CONJ-INF@@C.CONJ-POSS@ял V_SHUNY ; !!сетавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/овт:@C.CONJ-INF@@C.CONJ-POSS@ёвт V_SHUNY ; !!пановтны
@C.CONJ-INF@@C.CONJ-POSS@+Der/Asj:@C.CONJ-INF@@C.CONJ-POSS@ясь V_SHUNY ; !! босьтасьны


!! verb-to-noun
 
LEXICON VerbDer !!≈ * **@CODE@** 
 VerbDer-ONSET_OE ; !! FIX THIS 2015-09-06

LEXICON VerbDer_KYJNY !!≈ * **@CODE@**
:й VerbDer-ONSET_OE ; !!≈ * **@CODE@**
:й VerbDer-ONSET_JA ; !!≈ * **@CODE@** 
:й VerbDer-ONSET_JI ; !!≈ * **@CODE@**

LEXICON VerbDer_KUTNY !!≈ * **@CODE@**
 VerbDer-ONSET_OE ; !!≈ * **@CODE@**
 VerbDer-ONSET_A ; !!≈ * **@CODE@** 
 VerbDer-ONSET_JI ; !!≈ * **@CODE@**

LEXICON VerbDer_ARTASJNY !!≈ * **@CODE@**
: VerbDer-ONSET_OE ; !!≈ * **@CODE@**
: VerbDer-ONSET_JA ; !!≈ * **@CODE@** 
: VerbDer-ONSET_JI ; !!≈ * **@CODE@**

LEXICON VerbDer-ONSET_OE !!≈ * **@CODE@** 
+Der/ӧм+Event:%>ӧм Noun1 ;
+Der/ӧм+Event+N+Ins:%>ӧмӧн K ;
+Der/Omon+Ger:%>ӧмӧн K ;
+Der/Omon+Ger+Comp:%>ӧмӧнжык K ;
+Der/ісь+ActPrsPtc:%>%{иі%}сь N_ ; !!≈ * **@CODE@** kpv ысь

LEXICON VerbDer-ONSET_JI !!≈ * **@CODE@** 
  ! nonfinites
+Der/Ik+Ger+Cmpl:%>%{иі%}к K ; ! gerund no plural as in kpv (at the same time as the action)
!352 Pl
+Der/Ik+Ger+Ill:%>%{иі%}кӧ K ;
+Der/Ik+Ger+Ill+PxSg1:%>%{иі%}кам K ;
+Der/Ik+Ger+Ill+PxSg2:%>%{иі%}кат K ;
+Der/Ik+Ger+Ill+PxSg3:%>%{иі%}кас K ;
+Der/Ik+Ger+Ill+PxPl1:%>%{иі%}каным K ;
+Der/Ik+Ger+Ill+PxPl2:%>%{иі%}каныт K ;
+Der/Ik+Ger+Ill+PxPl3:%>%{иі%}каныс K ;


LEXICON VerbDer-ONSET_A !!≈ * **@CODE@** 
+ANA+Adv:%>%{ая%}на K ;
+ANAA+Adv:%>%{ая%}наа K ; !Adding after attestation in Komi Grammar 2000: 381
+AN+Der+Der/AdvCompMod:%>%{ая%}нмоз PossessiveSuffixes_NoSg1 ; !383

LEXICON VerbDer-ONSET_JA !!≈ * **@CODE@** 
+ANA+Adv:%>%{ая%}на K ;
+ANAA+Adv:%>%{ая%}наа K ; !Adding after attestation in Komi Grammar 2000: 381
+AN+Der+Der/AdvCompMod:%>%{ая%}нмоз PossessiveSuffixes_NoSg1 ; !383




LEXICON Verb-ynyDer
! verb-yny-to-verb
! The source stems have no stem vowel "ы", and therefore
! dervational suffixes come with an onset vowel

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim:@C.CONJ-INF@@C.CONJ-POSS@ышт V_SHUNY ; !!кольмыштны
!+Der/ышт+Dim+V:%>ышт  VerbConj ;
!+Der/ышт+Dim+V+Inf:%>ыштны K ;
!+Der/ышт+Dim+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ыштны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+Der/ышт+Dim+V+ConNeg@U.CONJ-CONNEG.YES@:%>ышт0@U.CONJ-CONNEG.YES@ WordEnd ;
!+Der/ышт+Dim+V+Imprt+Sg2:%>ышт WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ыштл V_VOLYNY ;   !!??кольмыштлыны
!+Der/ышт+Dim+V+Der/лы+V:%>ыштл  VerbConj ;
!+Der/ышт+Dim+V+Der/лы+V+Inf:%>ыштлыны K ;
!+Der/ышт+Dim+V+Der/лы+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ыштлыны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+Der/ышт+Dim+V+Der/лы+V+ConNeg@U.CONJ-CONNEG.YES@:%>ыштл00@U.CONJ-CONNEG.YES@ WordEnd ;
!+Der/ышт+Dim+V+Der/лы+V+Imprt+Sg2:%>ыштл WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ыштлывл V_VOLYNY ;  !!??кольмыштлывлыны

@C.CONJ-INF@@C.CONJ-POSS@+Der/л:@C.CONJ-INF@@C.CONJ-POSS@ыл V_SHUNY ;  !!тшӧктывны

@C.CONJ-INF@@C.CONJ-POSS@+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ывл V_VOLYNY ; !!тшӧктывлыны
!+Der/ывлы+V:%>ывл VerbConj ; ! мынтывл - мынтывлан
!+Der/ывлы+V+Inf:%>ывлыны K ;
!+Der/ывлы+V+ConNeg000@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ывлыны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+Der/ывлы+V+ConNeg0@U.CONJ-CONNEG.YES@:%>ывлы@U.CONJ-CONNEG.YES@ WordEnd ;
!+Der/ывлы+V+Imprt+Sg2:%>ывлы WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы:@C.CONJ-INF@@C.CONJ-POSS@ысь V_LYDDJYNY ; !! вевттьысьыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сь+Der/ышт+Dim:@C.CONJ-INF@@C.CONJ-POSS@ысьышт V_SHUNY ; !!лэптысьыштны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ысьыштлывл V_VOLYNY ; !!?? вевттьысьыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ысьыштл V_VOLYNY ; !!лэптысьыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сь+Der/л:@C.CONJ-INF@@C.CONJ-POSS@ысьл V_VOLYNY ; !!кутчысьлыны

@C.CONJ-INF@@C.CONJ-POSS@+Der/Al:@C.CONJ-INF@@C.CONJ-POSS@ал V_SHUNY ; !!бергӧдчавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/Al+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@авл V_VOLYNY ; !!бергӧдчавлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/Asj:@C.CONJ-INF@@C.CONJ-POSS@ась V_SHUNY ; !! бертласьны


!<!-- This far 2012-01-19 -->

!+Der/овт@C.CONJ-INF@@C.CONJ-POSS@:овт@C.CONJ-INF@@C.CONJ-POSS@ V_SHUNY ; !!пановтны


! Stray verb forms :-о


LEXICON VerbDer-V
+Der/тӧн:%>тӧн K ;
+v1+Der/тӧн+Ger+PxSg1:%>тӧнням K ;
+v2+Der/тӧн+Ger+PxSg1:%>тӧнним K ;
+v1+Der/тӧн+Ger+PxSg2:%>тӧннят K ;
+v2+Der/тӧн+Ger+PxSg2:%>тӧннит K ;
+v1+Der/тӧн+Ger+PxSg3:%>тӧнняс K ;
+v2+Der/тӧн+Ger+PxSg3:%>тӧннис K ;
+v1+Der/тӧн+Ger+PxPl1:%>тӧнняным K ;
+v2+Der/тӧн+Ger+PxPl1:%>тӧнниным K ;
+v1+Der/тӧн+Ger+PxPl2:%>тӧнняныт K ;
+v2+Der/тӧн+Ger+PxPl2:%>тӧнниныт K ;
+v1+Der/тӧн+Ger+PxPl3:%>тӧнняныс K ;
+v2+Der/тӧн+Ger+PxPl3:%>тӧнниныс K ;


! Page 346 in the Komi grammar
+Der/мысьт:%>мысьт K ; ! gerund (after the action) !353
+Der/мысь:%>мысь K ; ! !353
+Der/мӧн:%>мӧн PossessiveSuffixes_NoSg1 ; !399-403
+Der/мӧнъя:%>мӧнъя WordEnd ; !399-403
+Der/тӧм+VAbess:%>тӧм PossessiveSuffixes ;
+VCar:%>тӧг K ;
+VCar+PxSg1:%>тӧгъям K ;
+VCar+PxSg2:%>тӧгъят K ;
+VCar+PxSg3:%>тӧгъяс K ;
+VCar+PxPl1:%>тӧгъяным K ;
+VCar+PxPl2:%>тӧгъяныт K ;
+VCar+PxPl3:%>тӧгъяныс K ;
!+VCar:%>тӧг PossessiveSuffixes_NoSg1 ; ! 390

+VCar+Err/Orth:%>тӧгъя K ; !387
+VCar+Err/Orth:%>тӧга K ; !387
+VCar+Der+Der/AdvCompMod:%>тӧгмоз PossessiveSuffixes_NoSg1 ; !
+VAbess:%>тӧм A_tom ; 
 VTERI ;

LEXICON VTERI
+VTer:%>тӧдз K ;
+v1+VTer+PxSg1:%>тӧдззам K ;
+v2+VTer+PxSg1:%>тӧдззим K ;
+v1+VTer+PxSg2:%>тӧдззат K ;
+v2+VTer+PxSg2:%>тӧдззит K ;
+v1+VTer+PxSg3:%>тӧдззас K ;
+v2+VTer+PxSg3:%>тӧдззис K ;
+v1+VTer+PxPl1:%>тӧдззаным K ;
+v2+VTer+PxPl1:%>тӧдззиным K ;
+v1+VTer+PxPl2:%>тӧдззаныт K ;
+v2+VTer+PxPl2:%>тӧдззиныт K ;
+v1+VTer+PxPl3:%>тӧдззаныс K ;
+v2+VTer+PxPl3:%>тӧдззиныс K ;
!+VTer:%>тӧдз PossessiveSuffixes_NoSg1 ; !396


! 2012-02-11 This is a problem; олан-вӧлан, овны-вӧвны
!LEXICON VerbPRT ! one verb, вӧвны 
!: preterite ;
!@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+ConNeg+Pl3:@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны WordEnd ;
!@U.CONJ-CONNEG.YES@+ConNeg:@U.CONJ-CONNEG.YES@ WordEnd ;

LEXICON EMYSHT ! эмышт "is a little / are a few"
@U.CONJ-MX.IND@+Ind+Prs+Sg3:@U.CONJ-MX.IND@ WordEnd ;
@U.CONJ-MX.IND@@U.CONJ-NX.PL@@U.CONJ-PX.3@+Ind+Prs+Pl3:@U.CONJ-MX.IND@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
 
LEXICON GER ! 3 lexicalised +Ger forms.
+Ger: WordEnd ;

! vim: set ft=xfst-lexc:
